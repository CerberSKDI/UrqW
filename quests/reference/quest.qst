:menu
invkill
perkill
music="ATKML.mp3"

;Style_BackColor=13290186
;Style_BackColor=14407896
Instr Style_NonActiveText=Референс
;Style_dos_textcolor=#Style_dos_textcolor$
A=#Style_dos_textcolor#
B=#A$
pln Рефренс.
btn start,Начать 
btn author,Об авторе
;btn intro,intro
btn notes, Примечания

btn mgfwstart, test
btn minigamebridge, test
btn fishing, test
btn columnrealboom, test

end

:testitem
if not inv_Кольцо then inv+ Кольцо
music="ATKML.mp3"

btn testitem, 1
end

:testplay
Play splash.wav
btn testplay, 1
end

:notes
pln Некоторые предметы можно и нужно использовать.
pln Иногда (по абсолютно непонятным причинам) мини-игры глючат, но обычно после перезапуска все приходит в норму. И все-таки если вы застряли в мини-игре, то ее можно пропустить после трех неудачных попыток.
btn menu, Главное меню 
end

:realtest

music="push.mp3"
pause 200
music=0
btn realtest2, 1
end

:realtest2
music="push2.mp3"
pause 200
music=0
btn realtest, 12
end

:testr2
Play sword.wav
end

:test
instr Оружие=fishing.gif
pln #%Оружие$
pln <div align="center"><a href="btn:endfishing"><img src="#%Оружие$" class="foto" alt="Кликните когда круги сойдутся" border=”0?></a></div>
end



:testtest3
pln 123321

Play sword.mp3

end






:npchi
от=1
до=12
proc rnd()
if рнд=1 then pln - Чем могу помочь? 
if рнд=2 then pln - Добрый день.
if рнд=3 then pln - Здравствуйте.
if рнд=4 then pln - Что вам нужно?
if рнд=5 then pln - Да?
if рнд=6 then pln - Доброе утро.
if рнд=7 then pln - Здравствуйте.
if рнд=8 then pln - Добро пожаловать.
if рнд=9 then pln - Приветствую.
if рнд=10 then pln - Приветствую, герой.
if рнд=11 then pln - Добрый день, герой.
if рнд=12 then pln - Здравствуй, герой
end

:Use_Меч в камне_Использовать

if cantrowsword=1 then dragonisawake=1
if cantrowsword=1 then pln Замахнувшись, Бейл швырнул меч в дракона, угодив ему точно в глаз. Взревев, дракон вскочил на лапы и ринулся в сторону двери, Петр, испугавшись, резко закрыл ее и спрятался в углу. Потеряв из вида нападающего, дракон, сотрясая все стены в замке, немного потоптался на месте, а после прилег на прежнее место.
if cantrowsword=1 then inv- Меч в камне
if cantrowsword=0 then pln "И с помощью этого я должен победить дракона?" - обреченно подумал Петр, осматривая меч.
end


:Use_Телега с яблоками_Взять яблоко
inv+ Яблоко
end

:Use_Кольцо_Осмотреть
if ringdontlose=0 then pln Кольцо золотое, на нем нанесены какие-то непонятные надписи. "Надеюсь, Дейн за него отвалит столько золота, что я смогу вооружиться до зубов".
if ringdontlose=1 then pln Кольцо золотое, на нем нанесены какие-то непонятные надписи. "Может быть, я смогу продать его", - Петр покрутил кольцо в руках.
end

:Use_Ветка_Сделать удочку
if not inv_Крючок and not inv_Шнурок then pln "Надо раздобыть еще крючок и леску".
if not inv_Крючок and inv_Шнурок then pln "Еще нужен рыболовный крючок".
if not inv_Шнурок and inv_Крючок then pln "Надо найти веревку".

if inv_Крючок and inv_Шнурок then pln С помощью веревки, крючка, ветки и мусора под ногами Петр соорудил немного странную удочку. & fishrodmade=1 & inv- Ветка & inv- Крючок & inv+ Удочка & inv- Шнурок
end

:Use_Крючок_Сделать удочку
if not inv_Ветка and not inv_Шнурок then pln "Надо раздобыть еще палку и леску".
if not inv_Ветка and inv_Шнурок then pln "Неплохо бы еще найти какую-нибудь палку".
if not inv_Шнурок and inv_Ветка then pln "Надо найти веревку".

if inv_Ветка and inv_Шнурок then pln С помощью веревки, крючка, ветки и мусора под ногами Петр соорудил немного странную удочку. & fishrodmade=1 & inv- Ветка & inv- Крючок & inv+ Удочка & inv- Шнурок
end

:Use_Шнурок_Сделать удочку
if not inv_Крючок and not inv_Ветка then pln "Надо раздобыть еще крючок и палку".
if not inv_Крючок and inv_Ветка then pln "Еще нужен рыболовный крючок".
if not inv_Ветка and inv_Крючок then pln "Нужна прочная палка".

if inv_Ветка and inv_Крючок then pln С помощью веревки, крючка, ветки и мусора под ногами Петр соорудил немного странную удочку. & fishrodmade=1 & inv- Ветка & inv- Крючок & inv+ Удочка & inv- Шнурок 
end

:Use_Яблоко_Съесть
if inv_Здоровье=100 then pln Петр попытался надкусить яблоко, но не смог.
if inv_Здоровье<100 then pln "Надо же, на вкус как настоящее", - удивился Петр. & inv- Яблоко
if inv_Здоровье<100 then inv+ Здоровье
end

:Use_Дневник_Квесты
if count_village=0 then pln "Осмотрю это чуть позже" - решил Петр.<br>
if count_village>0 then pln <B>Главный квест.</B><br>
if count_village>0 then pln Спасти принцессу, заточенную в черном замке.<br>
if count_village>0 then pln --------------<br>
if questinntake=1 then pln <B>Побочные квесты.</B><br>
if questinntake=1 and questinndone=0 then pln Избавиться от крыс в подвале таверны.<br>
if questinndone=1 or questinndone=2 then pln <S>Избавиться от крыс в подвале таверны.</S><br>
if dekkquesttake=1 and gotfish=0 then pln Найти кольцо Кенаррда.<br> 
if dekkquesttake=1 and gotfish=1 then pln <S>Найти кольцо Кенаррда.</S><br>
if questkillwolftook=1 then pln Убить волков.<br>
jorneyshow=1
end

:common
if jorneyshow=1 then jorneyshow=0
end

;:Use_Дневник_Квесты
;if count_village=0 then pln "Осмотрю это чуть позже", - подумал Петр.
;if count_village>0 then pln Главный квест.
;if count_village>0 then pln Спасти принцессу заточенную в черном замке.
;if count_village>0 then pln --------------
;if questinntake=1 then pln Побочные квесты.
;if questinntake=1 then pln Избавиться от крыс в подвале таверны.
;end


Style_dos_textcolor=2
Style_dos_textcolor=#B$


:author
pln Звуки: freesound.org
pln Музыка: Kevin MacLeod (incompetech.com)
pln Все остальное: Шушкарт.
btn menu, Назад
end

:intro
pln Действие происходит в недалеком будущем. Петру Зимину, геймеру со стажем, внезапно подвернулась возможность протестировать игру на недавно разработанной системе виртуальной реальности, в этом ему помог Сергей, его старый друг-художник, работающий геймдизайнером в малоизвестной конторе по разработке игр.
btn menu, Назад
end

:character
pln Реальные:
pln Петр Зимин - главный герой, геймер со стажем. Судьба (в лице его старого друга Сергея) предоставила шанс опробовать первую игру созданную в системе виртуальной реальности
pln Сергей Хрумов - художник, друг Петра, работает геймдизанером в малоизвестной конторе по разработке игр.
pln Виртуальные:
pln Кеннард Дейн(хранитель знаний)
pln .....
btn menu, Назад
end

:start
goto prolog1
pln Сегодня
btn prolog1,1
end

:prolog1
pln Зеленоглазое такси, взвизгнув тормозами, заехало на тротуар и резко остановилось, едва не расплющив художнику ногу. За рулем сидела девушка, ее рыжие волосы волнами спадали на хрупкие плечи. Петр вспомнил около сотни дурацких шуточек про женщин и машины, но благоразумно решил промолчать, открыв дверь он сел на заднее сиденье. Сергей последовал за ним, захлопнув дверь, он назвал адрес. Машина резко тронулась с места, отбросив пассажиров назад.
pln - Теперь слушай внимательно, - художник повернулся к Петру. - Ты будешь подсоединен к системе через специальный шлем и ...
pln Но Петр пропускал его болтовню мимо ушей, все его внимание занимало зеркало заднего вида, которое было наклонено слишком низко и открывало прекрасный вид на грудь водителя.
btn prolog1.1, Далее
end

:prolog1.1
pln "Если бы декольте было бы еще чуть-чуть глубже, то я бы смог"... Такси внезапно тряхнуло на кочке и Петр, опомнившись, оторвал взгляд от зеркала.
pln - Итак ты все хорошо понял? - продолжал Сергей.
btn prolog2,- Э.. да, да.
btn prolog2.1, - Повтори еще раз, пожалуйста.
end

:prolog2.1
pln - Все просто, - вздохнул Серый, - тебе необходимо лишь соблюдать несколько правил, первое что...
pln "Хотя куда уж глубже, они и так почти полностью выпрыгивают на каждой кочке, и эта родинка на левой"... 
pln - Итак ты все хорошо понял? - продолжал Сергей.
pln Петр почувствовал дежавю. Игрушка, в виде черной кошки стоявшая на приборной панели автомобиля, покачивала головой в такт кочкам на дороге.
btn prolog2,- Да, конечно...
end

:prolog2
pln - Отлично, мы почти приехали. Видишь то серое здание? Это наш офис. 
pln Оплатив проезд, друзья вошли в здание через боковой вход. Немного попетляв по лабиринтам офисных коридоров они попали в небольшое помещение с множеством компьютеров и разной техники.
btn prolog3, Далее
end

:prolog3
pln Сергей нажал несколько кнопок на панелях, зажегся свет, компьютеры, пискнув, зашумели.
pln - Садись сюда, - Серый указал на кресло, обвитое проводами, шлем лежал рядом с ним, - и надень это.
pln Петр сел в кресло, его друг затянул застежки на руках и ногах. "Если игра будет про путешествие на марс, то я пропал" - немного испугался Петр.
pln - Теперь это - художник помог надеть шлем другу. - Когда будешь готов, нажми сюда, а затем сюда.
pln Нажав на копку, Петр ощутил резкий укол в области шеи и мир вдруг померк.
pln Через секунду перед глазами появилась надпись: 
pln 
pln 			                 "Dagondorn" 
pln 				"Нажмите войти, чтобы начать игру"
btn prolog4, Нажать.
end

:prolog4
music="RMbKML.mp3"
pln Мир взорвался неописуемым количеством красок превративший в нечто похожее на туннель. "Как будто сквозь звездные врата прошел" - подумал Петр. Но ни озноба, ни позывов к рвоте он не чувствовал. Спустя мгновение перед ним появилась голая земля, которая быстро обрастала травой и цветами, сзади из ничего появлялись деревья, черное небо постепенно становилось голубым. Недалеко впереди возникли очертания небольшого поселения, а затем на земле возникла тропинка, ведущая в него. Игрок появившийся на поляне отдаленно напоминал Петра, но одежда была явно из другой эпохи.
btn prolog5, Осмотреться
end

:prolog5
inv+ 3, Жизнь
inv+ 100, Здоровье
inv+ --------------
inv+ Дневник
inv+ Яблоко
inv+ 20, Золотых

pln Осмотревшись, Петр обнаружил около себя сумку, в которой находились дневник, яблоко и двадцать золотых. В дневнике отображалось текущее состояние героя, "Судя по записям меня зовут Петер Бейл, у меня три жизни, 100 здоровья, ноль опыта и первый уровень". 
pln Петр повертел в руках яблоко, "Интересно, какой у него вкус в виртуальности?", -  поднеся фрукт ко рту, он попытался его надкусить, но зубы лишь клацнули в воздухе.
pln - Эй, что за дела? У вас тут даже поесть нельзя? - сказал Бейл, обратившись к небу.
Style_dos_textcolor=2
p "Еда в игре восполняет здоровье, если оно максимально, то ничего съесть нельзя", & Style_dos_textcolor=#B$ & pln - голос Сергея возникал прямо в голове игрока.
Style_dos_textcolor=#B$
pln - Реалистично, - заключил Петр.
btn village, Идти по тропинке.
end

------------------------------------------------------------------------------------------------------------------



:village
if wasvillage=0 then pln Положив яблоко в сумку, и достав, оттуда, дневник Петер пошел вперед по тропе. "Ага, у меня есть главный квест, моя невеста похищена злым колдуном и заточена в замке под охраной огромного дракона, - листал журнал герой, - интересно, чем я ему так насолил?"
if wasvillage=0 then pln За чтением журнала игрок и не заметил, как оказался в центре небольшой деревни, состоявшей из семи-восьми домов, над дверьми некоторых их них блестели приколоченные вывески. В самом центре деревни находится большой колодец, около него стоит сгорбленный старик.
if wasvillage=1 then pln Петр находится у колодца в центре деревни.
wasvillage=1
btn smith, Зайти в кузницу
btn dekkard, Поговорить со стариком
btn inn, К таверне
btn footpath, Идти дальше по тропинке
end

:smith
if carttook=0 then p У входа в кузницу стоит небольшая пустая тележка. 
if carttook=0 then p Сама кузница 
if carttook=1 then p Кузница 
p представляет собой небольшое темное и пыльное помещение, в центре расположена наковальня, по которой безостановочно стучит огромным молотом кузнец. 
if count_smithbucket=0 then p Около входа лежит старое ржавое ведро. 
pln Слева у стены стоят стенды с оружием.
if wannastole=1 and count_smithbucket=0 then btn smithbucket, Взять ведро
btn smiths, Поговорить с кузнецом
btn weaponstorage, Подойти к стойке с оружием
if cartneedtake=1 and count_cartnearsmith=0 then btn cartnearsmith, Осмотреть телегу
btn village, Выйти
end

:cartnearsmith
carttook=1
inv+ Телега с яблоками
Style_dos_textcolor=2
pln "Не совсем то, что я имел в виду, но, наверное, сойдет".
Style_dos_textcolor=#B$
pln - И что мне с ней ... - Петр не договорить, как тележка до верха наполнилась, появившимися из ниоткуда яблоками. С опаской, взяв одно, Бейл надкусил его.
pln - Серьезно? Яблоки? Они же по одной единице здоровья восстанавливают.  - Петр оглядел повозку. - Сколько же придется съесть, штук 80?
Style_dos_textcolor=2
p "Ничего другого тут нет, & Style_dos_textcolor=#B$ & p - извинился Серый, & Style_dos_textcolor=2 & pln - Восполняй пока здоровья, а я попробую разобраться с мостом.
Style_dos_textcolor=#B$
pln - Смотри, не сломай ничего, - пробормотал Петр. - Хотя, сломать сломанный мост, даже тебе не под силу.
pln Вздохнув, Петер надкусил второе яблоко, затем третье, четвертое ... 
btn cartnearsmith2, Далее
end

:cartnearsmith2
pln <center>Спустя 80 яблок.</center>
btn cartnearsmith3, Далее
end

:cartnearsmith3
pln "Больше к ним в жизни не притронусь", - подумал Петр, выбросив очередной огрызок.

Style_dos_textcolor=2
pln "Пока ты ел, я мост починил, ну почти починил".
Style_dos_textcolor=#B$

pln - Да неужели?

inv_Здоровье=100
bringeisokay=1
goto smith
end

:smithbucket
pln Бейл подобрал с пола ржавое грязное ведро, в нем были старые гвозди, заклепки и прочий мусор, все это Петр вывалил на пол около стенда с оружием.
smithtrashonfloor=1
inv+ Ведро
goto smith
end

:weaponstorage
p К стене прислонена деревянная стойка, на ней висят несколько мечей. 
if smithtrashonfloor=1 then pln Около нее на полу лежат разные железки.

if dekkquesttake=1 and count_smiththrash=0 then btn smiththrash, Покопаться в мусоре на полу
btn weaponstoragestole, Украсть оружие.
btn smith, Назад
end

:smiththrash
pln В куче мусора Петр отыскал гнутый ржавый гвоздь.
inv+ Крючок
goto weaponstorage
end

:weaponstoragestole
if bucketonsmithhead=0 or bucketonsmithhead=2 then pln - Эй, я все вижу, даже не пытайся! - нахмурился кузнец.
if bucketonsmithhead=1 then pln - Эй, я все вижу, даже не пытайся! - крикнул кузнец, звякнув ведром.
if wannastole=0 and count_weaponstoragestole=1 then pln "У него шея толще моего бедра, не буду искушать судьбу", - решил Бейл.
if wannastole=1 and inv_Ведро=0 and bucketonsmithhead=0 then pln "Хм, а что если..." - Петр покосился на ведро в углу. & goto weaponstorage
if wannastole=1 and inv_Ведро=1 and bucketonsmithhead=0 then pln Петр покачал ведро в руке: "А что, стоит попробовать, надеюсь, кузнец не убьет меня". & goto weaponstorage
if bucketonsmithhead=1 then pln "Черт, пофиксили наверное, - сказал Петр сняв ведро с головы Чарса, - может еще пригодится". & bucketonsmithhead=2 & inv+ Ведро & haveweaponbucket=1 & goto weaponstorage
if wannastole=0 or bucketonsmithhead=2 then goto weaponstorage
end

:smiths
if smi=0 then pln Более мускулистого человека Петер еще никогда не видел, с каждым ударом молота вены на его руках набухали, так, как будто вот-вот лопнут. Над головой здоровяка висит надпись: "Чарс. Оружейник".
if namenpcknow=0 then pln "Эти таблички с имена выглядят по-идиотски, как будто в старую рпг попал", - отметил Петр. & namenpcknow=1
if count_smithsbuckethead=1 and bucketonsmithhead=1 then pln Чарс продолжает стучать молотом по наковальне, не замечая ведра на голове.
if smi=1 and bucketonsmithhead<>1 then pln Чарс работает, никак не реагируя на появление Бейла.
if count_smiths=3 then pln "Чем-то он мне Гимли напоминает, - подумал Петр, - наверное, шеей"

smi=1
btn allnpchi, - Привет
btn buyweapon, Купить оружие
if count_smithsbuckethead=0 and wannastole=1 and inv_Ведро then btn smithsbuckethead, Использовать ведро
btn smith, Назад
end

:smithsbuckethead
bucketonsmithhead=1
pln - Ты же не будешь против, если я тебе на голову ведро надену? - обратился Петр к Чарсу.
pln Чарс молча работал молотом, его мускулы набухали и перекатывались с каждым ударом.
pln "Возможно, сейчас я потеряю еще одну жизнь, - обреченно подумал Бейл, - хотя в скайриме это работало".
pln Петр, осторожно обойдя кузнеца, зажмурился и опустил ведро на голову Чарса. Кузнец продолжал работать, даже не обратив внимания на то, что его голову полностью закрывает старое грязное ведро.
pln "Отлично, теперь он ничего не видит, - обрадовался Петер, - хотя работать это ему не мешает"
inv- Ведро
goto smiths
end

:allnpchi
proc npchi
goto smiths

:buyweapon
pln Чарс показал Петру на стойку с оружием. Над предметами появились буквы и цифры.
pln Затупленный турнирный меч - 100 золотых
pln Бастардный клинок - 700 золотых
pln Ридилл - 999,99 золотых
if count_buyweapon=1 then pln "Тупой меч за 100 золотых! Да они издеваются", - возмутился Бейл.
if count_buyweapon=3 then pln "999,99 золотых - видно маркетинг и сюда добрался"

if inv_Золотых>99 then btn buywoodsword, Купить меч
btn smiths, Назад
end

:buywoodsword
pln Бейл дотронулся до надписи над клинком, через мгновенье меч уже висел у него за поясом, а кошель значительно полегчал.
boughtsword=1
inv+ Меч
inv- 100, Золотых
goto buyweapon
end

:inn
pln "Ежовый рыцарь" гласила вывеска, приколоченная к двери таверны. Над землей, слева от входа, парил большой голубой кристалл.
btn innin, Войти внутрь
btn crystal, Подойти к кристаллу
btn village, К колодцу
end

:crystal
if crys=0 then pln Кристалл парил над землей, вращаясь вокруг собственной оси. 
if crys=0 then pln - Это что еще за хрень?
Style_dos_textcolor=2
if crys=0 then p "Интерфейс взаимодействия с игрой, & Style_dos_textcolor=#B$ & p - раздался голос Сергея, - & Style_dos_textcolor=2 & p просто дотронься до него"
Style_dos_textcolor=#B$
if crys=1 then pln Петер прикоснулся к кристаллу & goto crystal2
if crys=0 then btn crystal2,  Коснуться
btn inn, Назад
crys=1
end

:crystal2
image=0
if crys2=0 then pln Яркая вспышка ослепила Бейла, кристалл засиял, и на нем проступили надписи. "Это похоже на главное меню, можно сохраниться и выйти", - понял Петер.
if crys2=1 then pln Кристалл засиял, и на нем проступили надписи.
crys2=1
btn inn, Отойти от кристалла
btn save, Сохранить игру
btn exit, Выйти из игры
end

:exit
if wannaexit=0 then pln Подумав, Петр решил не покидать игру так рано и еще немного осмотреться. & goto crystal2
if wannaexit=1 then pln Как только Петр дотронулся до кнопки выхода, у него перед глазами появилось окно с сообщением об ошибке. & MsgBox "Ошибка! //Ну и хрен с ней","Dagondorn.exe",urqError,s & goto exit2
if wannaexit=2 then pln Как только Петр дотронулся до кнопки выхода, у него перед глазами появилось окно с сообщением об ошибке. & image="error.jpg" & goto crystal2
if wannaexit=3 then pln Как только Петр дотронулся до кнопки выхода, у него перед глазами появилось окно с сообщением об ошибке. & image="error.jpg" & goto crystal2
end

:exit2
wannadie=1
wannaexit=2
pln - Это еще что?
Style_dos_textcolor=2
pln "Не знаю..."
Style_dos_textcolor=#B$
pln - Как мне теперь выйти-то?!
Style_dos_textcolor=2
p "Эээ... никак, наверное", & Style_dos_textcolor=#B$ & pln - задумался Серый.
pln - Просто сними с меня, этот чертов шлем! - яростно воскликнул Петр.
Style_dos_textcolor=2
pln "Я не могу, я же объяснял тебе, если тебя резко отключить ты можешь сойти с ума или даже умереть".
Style_dos_textcolor=#B$
pln - Тогда почини выход, в конце концов!
Style_dos_textcolor=2
pln "Я не знаю как, я же просто художник".
Style_dos_textcolor=#B$
pln - Так вызови программистов и пусть они все сделают!
Style_dos_textcolor=2
p "Тогда меня засудят и уволят, & Style_dos_textcolor=#B$ & p - вздохнул Сергей, - & Style_dos_textcolor=2 & pln ты сам меня уговорил незаконно провести тебя и показать игру, помнишь?"
Style_dos_textcolor=#B$
pln - Ты говорил, что она почти готова.
Style_dos_textcolor=2
pln "Ну, так и есть, в принципе".
Style_dos_textcolor=#B$
pln - Ладно, - сдался Бейл, - что мне делать?
Style_dos_textcolor=2
p "Попробуй умереть, возможно, игра просто выбросит тебя, & Style_dos_textcolor=#B$ & p  - предложил Сергей. - & Style_dos_textcolor=2 & p В игре можно умереть от падения, это быстро и, можно сказать, безболезненно".
Style_dos_textcolor=#B$
btn crystal2, Далее
end

:save
save crystal2
goto crystal2


:innin
if inv_Ведро с крысами then goto innbaseloserat
pln Скрипнув половицей, Петр вошел внутрь, помещение было чистым и светлым, но пустующим - здесь только хозяин, он стоит у стойки и протирает стаканы. За стойкой находится ведущая вниз лестница. 

btn inninman, Поговорить с трактирщиком
if questinntake=1 then btn inninladd, К лестнице
btn inn, Выйти
end

:inninman
if count_inninman=1 then pln "Чед. Трактирщик", - парила надпись над головой немного полного человека за стойкой. Над именем хозяина таверны висел в воздухе большой желтый восклицательный знак. "Похоже, у него есть квест для меня", - понял Петр.
if count_inninman>1 and questinntake=0 and questinndone=0 then pln Чед молча протирает стаканы. Над ним парит желтый восклицательный знак.
if count_inninman>1 and questinntake=1 and questinndone=0 then pln Трактирщик сидит и протирает стаканы. Над его именем парит серый вопросительный знак.
if questinndone=1 then pln Чед стоит за стойкой, над его головой висит желтый вопросительный знак.
if questinndone=2 then pln Чед стоит за стойкой и протирает стаканы.
if namenpcknow=0 then pln "Эти таблички с имена выглядят по-идиотски, как будто в старую рпг попал", - подумал Петр & namenpcknow=1

btn allnpchi2, - Приветствую
btn innmanstore, Посмотреть товары
if questinntake=0 then btn inninmanquest, - Я могу чем-то помочь?
if questinndone=1 then btn innmanquestratreward, - Я избавился от крыс
btn innin, Назад
end

:innmanstore
pln Хозяин таверны достал из-под прилавка флягу и яблоко, над ними засветились надписи и цифры. & if count_innmanstore=1 then pln "Какой богатый ассортимент", - вздохнул Бейл.
pln Яблоко - 10 золотых.
pln Фляга - 5 золотых.
if wasgurds=0 then pln "Думаю пока мне ничего не нужно", - решил Бейл.

if wasgurds=1 and count_instorewater=0 then btn instorewater, Купить флягу
btn inninman, Далее
end

:instorewater
pln Фляга, засветившись, исчезла с прилавка и появилась в рюкзаке Петра, а последние золотые испарились из кошелька.
inv+ Фляга
inv- 5, Золотых
goto innmanstore
end

:allnpchi2
proc npchi
goto inninman

:inninmanquest
pln - Да, в моем подвале завелись крысы, - трактирщик указал пальцем в сторону лестницы, - если избавишься от них, то я хорошо заплачу.
pln "Крысы в подвале? Более банального квеста и придумать нельзя", - разочарованно буркнул Петр.
questinntake=1
btn inninman, - Эм, ну ладно, наверное.
end

:innmanquestratreward
questinndone=2
pln "Надеюсь, это прокатит", - с надеждой подумал Бейл, сказав трактирщику, что выполнил задание.	
pln - Отлично, держи свою награду! - радостно сказал Чед, протянув несколько злотых.
inv+ 25, Золотых
btn inninman, - Спасибо
end

:inninladd
if count_inninlbasement>0 then goto inninlbasement
if count_inninladd=1 then pln Спустившись по лестнице, Петер уперся в стену, здесь не было ничего, кроме стены, и лестница вела прямо в нее.
Style_dos_textcolor=2
if count_inninladd=1 then p "Эм, так не должно быть, подожди, я попробую исправить. & Style_dos_textcolor=#B$ & p - смущенно проговорил Сергей. - & Style_dos_textcolor=2 & pln Я не программист, но простой объект наверняка смогу вставить"
Style_dos_textcolor=#B$
if count_inninladd=1 then pln Спустя пару секунд перед Бейлом появилась дверь.

if count_inninladd>1 and doordestr=0 then pln Петр стоит около двери.


if doordestr=0 then btn inninlbase, Открыть дверь
btn innin, Вернуться в таверну
end

:inninlbase
doordestr=1
pln Петр потянулся к ручке двери и дернул ее, дверь со скрипом обрушилась на него, за дверью возвышалась поросшая мхом стена.
pln - Ты поставил дверь, а дыру в стене забыл что ли? - прохрипел Петер из-под навалившейся на него двери.
Style_dos_textcolor=2
pln "Извини, сейчас новую поставлю".
Style_dos_textcolor=#B$
pln - Нет! Просто дыру сделай!
Style_dos_textcolor=2
pln "Ладно, ладно".
Style_dos_textcolor=#B$
pln В стене появилась дыра, а за ней лестница ведущая вниз.

btn inninlbasement, Спуститься
end

:inninlbasement
p Подвал освещается двумя факелами, висящими на стенах, в одном углу друг на друге лежат несколько небольших мешков с углем, а в другом стоит небольшой деревянный сундук. & if baseratexist=0 then pln По полу ползают пять больших крыс.
if wannaexit=3 and haveweaponbucket=0 and count_innbaserat=0 then pln "Не буду же я убивать их голыми руками? - поморщился Петр. - Надо раздобыть какое-нибудь оружие" 
if count_inninlbasement=1 then pln - И чем же ты прикажешь их убивать? - громко сказал Петер.
if count_inninlbasement=1 then Style_dos_textcolor=2
if count_inninlbasement=1 then pln "У тебя должен быть кинжал в сумке".
if count_inninlbasement=1 then Style_dos_textcolor=#B$
if count_inninlbasement=1 then pln - Там только яблоко и дневник! - Петр еще раз обыскал сумку.
if count_inninlbasement=1 then Style_dos_textcolor=2
if count_inninlbasement=1 then pln "Ну, возможно и тут какой-то баг".
if count_inninlbasement=1 then Style_dos_textcolor=#B$
if count_inninlbasement=1 then pln - Ладно, мне надоел этот фарс, как выйти отсюда? - раздраженно ответил Бейл, потирая ушибленную дверью коленку.
if count_inninlbasement=1 then Style_dos_textcolor=2
if count_inninlbasement=1 then p "С помощью кристалла у таверны", & Style_dos_textcolor=#B$ & pln - вздохнул Сергей. & wannaexit=1
if count_inninlbasement=1 then Style_dos_textcolor=#B$

if needkaboom=1 and count_inninbasecoal=0 then btn inninbasecoal, Взять мешок
if tookropefromchest=0 then btn innbasechest, Осмотреть сундук
if haveweaponbucket=1 and count_innbaserat=0 then btn innbaserat, Собрать крыс в ведро

btn innin, Подняться по лестнице
end

:inninbasecoal
inv+ Мешок с углем
pln Петр взвалил один мешок на плечо.
goto inninlbasement
end

:innbasechest
if count_innbasechest=1 then pln Сундук оказался незапертым, внутри Бейл нашел несколько золотых и длинный крепкий шнурок. Прихватив золото, Бейл закрыл сундук.
if count_innbasechest>1 and dekkquesttake=0 then pln На дне сундука лежит небольшой, но длинный и крепкий шнурок.
if dekkquesttake=1 then pln "Это может пригодиться", - подумал Петр, взяв шнурок. & tookropefromchest=1 & inv+ Шнурок

if count_innbasechest=1 then inv+ 20, Золотых
goto inninlbasement
end

:innbaserat
baseratexist=1
pln После нескольких попыток Петр смог поймать пару крыс, "А это не так уж и трудно". Спустя полчаса в ведре были все пять крыс.
Style_dos_textcolor=6
pln Квест выполнен!
Style_dos_textcolor=#B$

Play quest.mp3

inv- Ведро
inv+ Ведро с крысами
questinndone=1
goto inninlbasement
end

:innbaseloserat
baseratexist=0
inv- Ведро с крысами
inv- 5, Здоровье
pln Взяв в руки ведро с пищащими крысами, Петр начал подъем по лестнице. На предпоследней ступеньке одна из крыс извернулась и укусила Бейла за палец, "Твою ж..." - воскликнул он выпустив из рук ведро, крысы радостно пискнув, побежали вниз по лестнице.

Style_dos_textcolor=4
pln -5 Здоровья
Style_dos_textcolor=#B$

pln Кровь медленно стекала по пальцу, "Второй раз я этого делать не буду", - Петр яростно пнул пустое ведро, и оно с шумом полетело вслед за крысами. 
btn innin, Далее
end





------------------------------------------------------------------------------------------------------------------


:footpath
pln Через несколько минут Петр дошел до места где дорога разделяется на три небольшие тропинки, одна ведет скалистому утесу, другая в небольшой лес, а третья к озеру, поблескивающему на горизонте.
if banditssaws=1 and count_footpathbandits=1 then pln "Кольцо потерял, а теперь еще и в деревню не пускают, - отчаявшись, подумал Бейл, - что же мне делать?" & banditssaws=0

btn cliff, К утесу
btn lake, К озеру
btn forestfake, В лес
if gotfish=0 or killbandits=1 then btn village, В деревню
if gotfish=1 and killbandits=0 then btn footpathbandits, В деревню
end

:footpathbandits
if count_footpathbandits=1 then banditssaws=1
knowbanditsonroad=1
pln На полпути к деревне Бейла остановили два вооруженных человека, внушительных размеров мужчина и хрупкая девушка, Лэни "Разбойник" и Минелла "Грабительница" гласили надписи над их головами.
pln - Гони кольцо, придурок, - громко сказала милая на вид девушка.
pln - Будьте добры кольцо, милорд, - поигрывая кинжалом, произнес разбойник.
if count_footpathbandits=1 then pln "Алло, это прачечная?" - пронеслась мысль в голове Петра.

if inv_Камень then btn pathbanditsrock, Дать им камень
btn footpath, Бежать назад
end

:pathbanditsrock
killbandits=1
pln "С чем черт не шутит", - Бейл протянул камень разбойникам:
pln - Вот кольцо!
pln Получив камень разбойники молча ушли в сторону леса, и дойдя до опушки растворились в воздухе.
inv- Камень
btn village, Продолжить путь в деревню
end

:cliff
pln Тропа заканчивается у обрыва, откуда-то снизу раздается шум прибоя. Петр посмотрел вниз, высота ошеломляла, волны разбивались о скалы, нашептывая непонятные слова. У самого края утеса стоит высокое дерево с длинными ветвями, покрытыми густой листвой.

if wannadie=1 then btn cliffdie, Спрыгнуть
if dekkquesttake=1 and count_clifftree=0 then btn clifftree, Отломать ветку у дерева
btn footpath, Назад
end

:clifftree
pln Отыскав подходящую ветку, Петр отломал ее.
inv+ Ветка
goto cliff
end

:cliffdie
music="BVKML.mp3"
pln - Будет больно? 
Style_dos_textcolor=2
pln "Наверное". 
Style_dos_textcolor=#B$
pln "Поддержал, блин, - Петр еще раз посмотрел с обрыва, - господи, как же тут высоко".

pln Разбежавшись, Бейл прыгнул со скалы, полет был недолгим, но красочным, немного не долетев до спасительной голубой кромки воды, Петр расшибся об острые скалы, в момент удара мучительная боль пронзила его, словно кинжалом, и разлилась по всему телу. 
btn cliffdie2, Далее
end


:cliffdie2
music="RMbKML.mp3"
wannaexit=3
wannadie=2
wannastole=1
inv- Жизнь
pln Спустя несколько минут Бейл очнулся в центре деревни около колодца, все его тело ныло и болело, с трудом поднявшись на ноги, он отряхнулся от грязи и пыли.
Style_dos_textcolor=2
pln "Так, я понял, игра будет воскрешать тебя здесь, пока не закончатся жизни".
Style_dos_textcolor=#B$
pln - То есть мне придется еще два раза пережить это?
Style_dos_textcolor=2
pln "Не думаю, что это поможет, после того как кончатся все жизни, ты попадешь в главное меню, но так как выход не работает, ты не сможешь выйти, только начать игру заново, но без обновления мира это чревато серьезными ошибками".
Style_dos_textcolor=#B$
pln - Более серьезными, чем уже есть? - заметил Петр, - Да вы превзошли себя. Зачем я вообще убил себя?
Style_dos_textcolor=2
pln "Ну, попытка не пытка".
Style_dos_textcolor=#B$
pln "Ага, как же", - подумал Петер, потирая ушибленное плечо, но вслух сказал: 
pln - И что же мне теперь делать? 
Style_dos_textcolor=2
pln "Постарайся пройти игру, не умирая".
Style_dos_textcolor=#B$
pln - Но разве после завершения игры я не попаду в то же главное меню? - усомнился Петер.
Style_dos_textcolor=2
pln "Нет, после убийства дракона и спасения принцессы игра просто выкинет тебя, мы это проверяли... вроде бы".
Style_dos_textcolor=#B$
pln - Обнадеживает, - вздохнул Бейл. - И как же мне убить дракона?
Style_dos_textcolor=2
pln "Сначала попробуй раздобыть оружие".
Style_dos_textcolor=#B$
goto village
end



------------------------------------------------------------------------------------------------------------------

:dekkard
;if previous_loc=dekkquestfishfindagain then pln "Всего лишь 5 золотых осталось, где бы их теперь достать?".

if count_dekkard=1 then pln Старик стоит, облокотившись на край колодца, над его головой парят буквы: "Кенаррд Дейн. Хранитель знаний". 
if count_dekkard<>1 then p Дейн стоит у колодца. 
if questinndone=2 and dekkquesttake=0 then p Над ним парит желтый восклицательный знак.
if questinndone=2 and dekkquesttake=1 and count_dekkquestfishfind=0 then p Над его головой парит серый вопросительный знак.
if namenpcknow=0 then pln "Эти таблички с имена выглядят по-идиотски, как будто в старую рпг попал", - подумал Петр. & namenpcknow=1
btn allnpchi3, - Здравствуйте
if questinndone=2 and dekkquesttake=0 then btn dekkquest, - Чем могу помочь?
if gotfish=1 and count_dekkquestfishfind=0 then btn dekkquestfishfind, - Эм, я как бы нашел кольцо.
if count_lakeringtruesearch=1 and count_dekkquestfishfindagain=0 then btn dekkquestfishfindagain, - Эм, я как бы нашел кольцо, еще раз
btn village, Отойти от старика
end

:allnpchi3
proc npchi
goto dekkard

:dekkquest
dekkquesttake=1
pln - Неподалеку от нашей деревни есть небольшое озеро, уже много лет там обитает одна очень хитрая рыба. Если верить легендам, которые рассказывал мой отец, ее можно поймать только на наживку ... - начал рассказ Дейн.
pln - Старик, простой дай мне, чертов квест, - перебил герой.
pln Но Кеннард не обратил никакого внимания на нетерпеливость Бейла и продолжил свой рассказ, который Петр, в свою очередь, пропустил мимо ушей, поняв лишь то, что ему надо достать кольцо из рыбы заглотившей его.
btn dekkard, - Дальше
end

:dekkquestfishfind
dekkquestpassed=1
inv+ 20, Золотых
pln Старик обрадовался и извлек из кармана несколько монет:
pln - Спасибо, герой! Вот твоя награда.
pln "Всего лишь 15 золотых осталось, где бы их теперь достать?", - задумался Бейл.
btn dekkard, - Да не за что
end

:dekkquestfishfindagain
inv- Кольцо
inv+ 20, Золотых
pln Старик обрадовался и извлек из кармана несколько монет:
pln - Спасибо, герой! Вот твоя награда.
btn dekkard, - Да не за что, серьезно
end

------------------------------------------------------------------------------------------------------------------


--------------------
ОЗЕРО

:lake
pln Подойдя к небольшому водоему, с крутым каменистым берегом, Петр присел на траву, греясь на солнце.
if fishrodmade=0 and dekkquesttake=1 then pln "Надо найти удочку или что-то типа того", - Бейл обшарил карманы, словно надеялся обнаружить там набор рыболова.
Style_dos_textcolor=2
if dekkquestpassed=1 and count_lakeringtruesearch=0 then pln "Смотри, там между камнями что-то блестит".
Style_dos_textcolor=#B$


btn footpath, Вернуться
if knowbanditsonroad=1 and count_lakerock=0 then btn lakerock, Взять камень с берега
if gotfish=0 and dekkquesttake=1 and fishrodmade=1 then btn fishing, Порыбачить
if ringdontlose=1 and dekkquestpassed=0 and count_lakeringsearch<4 then btn lakeringsearch, Поискать кольцо
if dekkquestpassed=1 and count_lakeringtruesearch=0 then btn lakeringtruesearch, Осмотреть камни на берегу

end

:lakerock
pln Нагнувшись, Бейл подобрал один из множества камней лежавших на берегу.
inv+ Камень
goto lake
end

:lakeringsearch
pln Петр осмотрел камни под ногами, но ничего не нашел.
if count_lakeringsearch=4 then pln "Я уже тут все обыскал, видимо оно упало в озеро, но там его уже не достать", - вздохнул Петер.
goto lake
end

:lakeringtruesearch
pln "И как оно сюда закатилось?" - герой приподнял камень около берега, кольцо лежало в воде, блестя на солнце.
inv+ Кольцо
goto lake
end


:fishing


hide_use=1

play splash.wav
pln <div align="center"><a href="btn:endfishing"><img src="fishing.gif" class="foto" alt="Кликните когда круги сойдутся" border=”0?></a></div>

cathfish=0
pause 1000
pause 1000
pause 1000
cathfish=1
pause 900
cathfish=2
pause 1000
cathfish=3
goto endfishing
end

:endfishing
hide_use=0
if cathfish=1 then pln "Кажется, поймал что-то", - немного удивился Бейл. & goto fishrnd
if cathfish=0 then pln Петр вытащил удочку слишком рано и ничего не поймал. & goto fishagain
if cathfish=2 then pln "Проклятье, сорвалась!" - выругался Петр. & goto fishagain
if cathfish=3 then pln "Проклятье, сорвалась!" - выругался Петр. & goto fishagain
end

:fishrnd
от=1
до=6
proc rnd()
if рнд=1 then p Вытащив крючок из воды, Петр увидал, что на нем болтается огромная рыба.  & gotfish=1 & inv+ Кольцо & goto lakefishringgot
if рнд=2 then p Вытащив крючок из воды, Петр увидал, что на нем болтается огромная рыба.  & gotfish=1 & inv+ Кольцо & goto lakefishringgot
if рнд=3 then p Вытащив крючок из воды, Петр увидал, что на нем болтается огромная рыба.  & gotfish=1 & inv+ Кольцо & goto lakefishringgot
if рнд=6 then p Вытащив крючок из воды, Петр увидал, что на нем болтается огромная рыба.  & gotfish=1 & inv+ Кольцо & goto lakefishringgot

if рнд=4 then pln Вытащив крючок из воды, Бейл увидел болтающийся на нем сапог. "Ну да, классика же", - Петр, зашвырнув сапог в кусты. 
if рнд=5 then pln Петр, выудив из воды пучок водорослей и пару веток со дна озера, разочарованно выбросил их в кусты.
goto fishagain
end

:fishagain
btn fishing, Закинуть удочку еще раз
if count_fishagain>2 then btn lakefishringgot, Пропустить мини-игру
btn lake, Назад
end

:lakefishringgot
gotfish=1 
if not inv_Кольцо then inv+ Кольцо
pln Ударив хвостом по лицу Бейла, она выскользнула из его рук и упала наземь. Едва коснувшись земли, рыба испарилась, и на ее месте появилось блестящее кольцо.
pln "Ну, хоть открывать ее не пришлось", - обрадовался Петр, подняв кольцо.
Style_dos_textcolor=6
pln Квест выполнен!
Style_dos_textcolor=#B$

Play quest.mp3

btn lakefishringgot2, Отойти от берега
end

:lakefishringgot2
ringdontlose=1
inv- Кольцо
inv- Удочка
pln Оставив удочку лежать на берегу, Бейл направился в сторону развилки, но не успел он пройти и пяти шагов, как споткнулся о камень и распластался на жесткой земле, кольцо, вылетев у него из рук, укатилось в сторону.

Style_dos_textcolor=2
p "Ну ты и лох", & Style_dos_textcolor=#B$ & pln - прокомментировал Сергей.
Style_dos_textcolor=#B$
pln - Могу поспорить, это было заскриптовано, - отряхиваясь, оправдывался Петр.
Style_dos_textcolor=2
p "Нет, не было", & Style_dos_textcolor=#B$ & pln - безжалостно добил Серый.
Style_dos_textcolor=#B$


btn lake, Далее
end




------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------


:forest
if boughtsword=0 then goto forestfake
if count_forestendfake=0 then pln Лес был небольшим и состоял всего лишь из пару десятков деревьев и столько же кустарников. Из ближайших кустов слышен шум воды, скорее всего там протекает небольшой ручей. На опушке леса бродят шесть-семь животных похожих на собак, но немного больше, над их головами витают красные надписи "Серый волк".
if count_forestendfake>0 then pln Выйдя к опушке леса, Петр заметил бродящих неподалеку несколько "Серых волков".

if count_forest=1 then pln "Они просто топчутся на месте, но если я подойду к ним, думаю, они нападут, - почесал затылок Петр. - Тропу через лес перекрывают только двое, скорее всего я с ними справлюсь".

if winforstwolfs=0 then btn forestendfake, Идти дальше по тропе
if winforstwolfs=1 then btn forestend, Идти к реке
if swordisbroke=1 and count_forestclearing=0 then btn forestclearing, На поляну

if count_riverstream=0 then btn riverstream, К роднику
if count_riverstream>0 then btn riverstream, К серному источнику

btn footpath, К развилке
end

:forestclearing
swordisbroke=2

Style_dos_textcolor=2

Style_dos_textcolor=#B$


pln - И что ты тут хотел мне показать? - спросил Петр, дойдя до места.
Style_dos_textcolor=2
p "Секунду", & Style_dos_textcolor=#B$ & pln - сказал Сергей.
pln "Секунда" длилась почти пять минут, и Бейл нетерпеливо топтался на месте. Когда он уже почти потерял терпение, перед ним, в центре поляны, вырос из-под земли большой камень и меч, воткнутый в него.
pln - И зачем ты его в камень засунул?
Style_dos_textcolor=2
pln "Традиция же, не бойся, ты его легко вытащишь".
Style_dos_textcolor=#B$
pln Петр, недоверчиво посмотрев на меч в камне, осторожно подошел к нему. Взявшись за рукоятку и дернув ее изо всех сил, Петр улетел в кусты вместе, как и с мечом, так и с камнем, болтающимся на острие клинка. Выбравшись из колючего кустарника, Бейл осмотрел оружие - камень все еще висел на конце меча, более того камень и клинок были единым целым, и разделить их не представлялось возможным. Но весила вся эта диковинная конструкция не более килограмма.
pln - И что мне с этим делать? - Бейл дотронулся	 до острия торчавшего из камня. - Он еще и тупой как пробка!
Style_dos_textcolor=2
pln "Эм, кажется, я переборщил с легкостью".
Style_dos_textcolor=#B$
pln - Ага, и с каменистостью тоже, - пробурчал Петр.
Style_dos_textcolor=2
p "С чем? Ладно, с оружием не получилось, но кажется, я смогу поправить тебе здоровье, & Style_dos_textcolor=#B$ & p  - пообещал Сергей. -  & Style_dos_textcolor=2 & pln Найди какую-нибудь емкость, пакет, корзину, или еще что-нибудь".
Style_dos_textcolor=#B$

inv+ Меч в камне
;pln 123321 "Нда, с мечом не получилось, но кажется я смогу поправить тебе здоровье. Найди какую-нибудь емкость, пакет или корзину или еще что-нибудь."
cartneedtake=1
btn forest, - Ладно
end

:forestfake
if boughtsword=1 then goto forest
pln Подойдя к лесу, Петр заметил у тропы бродят несколько волков.

Style_dos_textcolor=2
p "Сюда лучше не соваться без оружия", & Style_dos_textcolor=#B$ & pln - предупредил Сергей.
Style_dos_textcolor=#B$

btn footpath, - Ясно
end

:forestendfake
pln Едва Бейл подошел к тропе два волка с воем бросились к нему.
btn mgfwstart, Обнажить меч
end

:forestendfake2
music=0
music="RMbKML.mp3"


hide_use=0

winforstwolfs=1
swordisbroke=1
inv- 76, Здоровье
inv- Меч

pln Осмотревшись, Бейл понял, что потерял в бою не только меч, но и значительную часть здоровья, но, к счастью, раны уже не кровоточили и начинали затягиваться.
Style_dos_textcolor=4
pln -76 Здоровья
Style_dos_textcolor=#B$

Style_dos_textcolor=2
pln "Эй, да ты меч сломал"
Style_dos_textcolor=#B$
pln - Да ты что, - раздраженно ответил Петр.
Style_dos_textcolor=2
p "Тебе повезло, я нашел, как тут оружие добавлять, & Style_dos_textcolor=#B$ & p - радостно сообщил Серый. - & Style_dos_textcolor=2 & pln Я смогу сделать тебе новый меч! Нам нужно немного места, поляна около входа в лес отлично подойдет".
Style_dos_textcolor=#B$
pln - И почему мне не нравится эта идея?

btn forestend, Далее
end

:forestend
;if count_forestend=1 then proc forestend2note

p Тропа через лес заканчивается у обрушенного моста, рядом с которым стоит 
if count_riverman=0 then pln незнакомец.
if count_riverman>0 then pln лодочник.

if bringeisokay=0 then btn rivebridge, Осмотреть мост
if bringeisokay=1 and brigebrokeagain=0 then btn rivebridgenew, Перейти через мост
if brigebrokeagain=1 and count_bridgeguarddeath=0 then btn rivebridgenew, Переплыть речку на лодке
if brigebrokeagain=1 and count_bridgeguarddeath>0 then btn castelyard, К замку

btn riverman, Поговорить с лодочником
btn forest, В лес
end


;:forestend2note
;pln 123321 "Тут неподалеку есть исцеляющий родник"
;end

:riverman
questkillwolftook=1
if count_riverman=1 then pln "Джон Нотлок. Лодочник" висела надпись над головой незнакомца, но ни лодки, ни парома, ни даже плота поблизости не было. Когда Петр подошел к нему, лодочник произнес:
if count_riverman=1 then pln - Я переправлю тебя через реку, если принесешь мне пять шкур серых волков.
if count_riverman>1 then pln Джон, не шевелясь, стоит у реки.

if count_riverman=1 then btn forestend, Понятно
if count_riverman>1 then btn forestend, Отойти
end

:rivebridge
pln Подойдя к полностью разрушенному мосту, Петер посмотрел вниз, река не очень бурная, но достаточно широкая и переплыть ее не получится.
btn forestend, Назад
end

:riverstream
if count_riverstream=1 then pln Пробравшись сквозь кусты, Бейл нашел небольшой родник со странной, дурно пахнущей водой. Пройдя немного вверх по течению, Петр обнаружил, что родник, берет начало у месторождения серы, желтые кристаллы, усеяв все поле, отравляли воду.
if count_riverstream>1 then pln Сморщив нос, Петр подошел к серному источнику.


Style_dos_textcolor=2
if count_riverstream=1 and wasgurds=0 then pln "Лучше не пей ее, у тебя и так здоровья почти нет".
if wasgurds=1 and ssaidtoxic=0 then p "Кажется эта вода ядовитая", & Style_dos_textcolor=#B$ & pln - предупредил Серый. & ssaidtoxic=1
Style_dos_textcolor=#B$

if candig=1 and count_riverstreamsulfur=0 then btn riverstreamsulfur, Накопать серы
if inv_Фляга then btn rivstslwtrtake, Наполнить флягу водой
btn forest, К опушке леса
end

:rivstslwtrtake
pln Склонившись над источником, Петр набрал полную флягу серной воды.
inv- Фляга
inv+ Фляга с водой
goto riverstream
end

:riverstreamsulfur
pln Поработав немного лопатой, Бейл накопал полное ведро серы.
inv+ Ведро серы
goto riverstream
end

;:riverstream2
;pln 123321 серный источник
;inv- 20, Здоровье
;pln "Нда, со здоровьем не получилось, но зато я понял как тебя вооружить!"
;btn forestend, К реке
;end


:forestend2fake
brigebrokeagain=1
if count_forestend2fake=1 then pln Подойдя к сломанному мосту, Петр заметил лодку около берега, переплыв на другую сторону, он затащил ее на берег, чтобы не унесло течением.
if count_forestend2fake>1 then pln Петр переплыл реку на лодке.
goto forestend
end


------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------

:rivebridgenew
if mgbgwontr=0 then goto rivebridgenew2fake

pln Перейдя реку, и пройдя немного по дороге, Бейл остановился около ворот ведущих в замок, они были открыты, но их охраняли двое стражников. Они сидели за столом около ворот, и пили вино.
if wasgurds=0 then pln Петр попытался пройти в замок, но стража, преградив ему путь, и обнажив оружие, не пустила его. & wasgurds=1

btn forestend2fake, В лес
;if count_rivebridgenew>1 then btn forestend, В лес
if inv_Фляга с водой then btn bridgeguarddeath, Подлить воду в вино
end

:rivebridgenew2fake
inv- Телега с яблоками
inv+ 5, Яблоко
Style_dos_textcolor=2
p "Я смог починить мост", & Style_dos_textcolor=#B$ & pln - начал Сергей.
Style_dos_textcolor=#B$
pln - Но? У тебя всегда есть какое-нибудь "но".
Style_dos_textcolor=2
pln "Но он почему-то опять рушится через пару минут, тебе придется быстро перебежать по мосту пока он снова не обрушился".
Style_dos_textcolor=#B$
pln Взяв несколько яблок с телеги, Бейл оставил ее около реки, бежать вместе с ней было бы невозможно.

btn minigamebridge, - Понятно
end

:rivebridgenew3fake
hide_use=0

music="RMbKML.mp3"
mgbgwontr=1
goto rivebridgenew
end

:bridgeguarddeath
inv- Фляга с водой
pln Осторожно подойдя к столу и вылив содержимое фляги в бутылку с вином, Петр спрятался за углом.
pln Через несколько минут, стражники, выпив отравленное вино, упали на землю и бились в агонии пока не растворились в воздухе.
btn castelyard, Пройти дальше
end

:castelyard
cantrowsword=0
if count_castelyard=1 then pln Замок был небольшим и вовсе не черным, как писалось в журнале, пройдя через двор замка, Бейл вошел внутрь. 
if count_castelyard>1 then p Петр переступил порог замка. 
pln Изнутри замок казался еще меньше чем снаружи, на стенах висели гобелены, на полу лежал огромный ковер. Слева от входа находилась винтовая лестница, а прямо перед Бейлом стояла большая дверь.


btn castledragon, Открыть дверь
btn castleupstairs, Подняться вверх по лестнице
btn castledownstairs, Спуститься вниз
btn forestend, Выйти из замка
end

:castledragon
cantrowsword=1
pln За дверью слышен оглушающий храп, приоткрыв ее, и заглянув внутрь, Петр увидел громадного дракона, спящего посреди не менее большого зала. Стены зала были очень древние - все в трещинах, потолок едва держался на них, пол был также не в лучшем состоянии.
btn castelyard, Назад
end

:castledownstairs
if count_castledownstairs=1 then pln Спустившись вниз, Петр оказался в подвале замка, к стенам были прикреплены полки с колбами и банками, а рядом стоял стол с реагентами. "Скорее всего, это алхимическая лаборатория", - предположил Бейл. Сверху из-под потолка огромного зала, раздавался храп дракона, который поддерживали четыре колонны расположенные по углам подвала.
if count_castledownstairs>1 then pln Бейл в подвале замка. 
btn casdnwlab, Осмотреть лабораторию
if needkaboom=0 then btn casdnwcolumn, Осмотреть колонны
if needkaboom=1 then btn casdnwcolumn, Подойти к колонне

btn castelyard, Подняться наверх
end

:casdnwcolumn
if dragonisawake=1 then needkaboom=1
if powderisready=1 then pln Петер подошел колонне, около нее лежит мешок с порохом.
if powderisready=0 and dragonisawake=0 then pln Подойдя к колоннам, Бейл осмотрел их, на вид они были довольно старые, но все еще выдерживали огромный вес потолка.
if powderisready=0 and dragonisawake=1 then pln Осмотрев колонны, Петр заметил, что у одной из них имеется большая трещина посередине и выглядит она намного менее крепкой, чем остальные. 
if powderisready=0 and dragonisawake=1 and saidwisidea=0 then pln "Наверное, дракон прошелся как раз над ней, если ее сломать, то возможно, потолок обвалится, - задумался Бейл. - В лаборатории наверняка можно найти что-нибудь взрывоопасное" & saidwisidea=1


if powderisready=0 and inv_Мешок с порохом then btn casdnwcolumnboom, Взорвать колонну
if powderisready=1 and inv_Огниво then btn columnrealboom, Поджечь порох
btn castledownstairs, Отойти от колонны
end

:casdnwcolumnboom
pln Положив мешок у основания колонны, Петр провел дорожку из пороха к выходу, "Все готово, можно взрывать!" - обрадовался он.
btn columnboom2, Поджечь порох
end

:columnboom2
powderisready=1
pln "Стоп, а чем поджигать-то?" - внезапно задумался Петр.
btn casdnwcolumn, Назад
end

:casdnwlab
if count_casdnwlab=1 then pln Стол уставлен непонятными предметами, колбами, мерными стаканами, весами и прочим. На полках разложены различные химические реагенты.
if count_casdnwlab>1 then pln Петр стоит у стола с реагентами.


if needkaboom=1 and caslabwas=0 then pln На одном из сосудов Бейл заметил надпись: "Salt of Peter". 
if needkaboom=1 and caslabwas=0 then pln - Смотри, соль названа в мою честь! - обрадовано сообщил Петр.
Style_dos_textcolor=2
if needkaboom=1 and caslabwas=0 then p "Это селитра, наркоман", & Style_dos_textcolor=#B$ & pln - сказал Сергей. & caslabwas=1
Style_dos_textcolor=#B$

if needkaboom=1 and count_casdnwlabcel=0 then btn casdnwlabcel, Взять банку селитры
if powderisready=1 and count_casdnwlabfire=0 then btn casdnwlabfire, Поискать спички
if inv_Ведро серы and inv_Мешок с углем and inv_Селитра and count_casdnwlabmp=0 then btn casdnwlabmp, Сделать порох
btn castledownstairs, Назад
end

:casdnwlabmp
pln Разложив все ингредиенты на столе, Бейл задумался: "Чем больше угля, тем лучше горит, ведь так?". 
pln - Эй, как там правильно порох делать? 
Style_dos_textcolor=2
pln "Селитры побольше, серы и угля поменьше", - после небольшой паузы ответил Серый.
Style_dos_textcolor=#B$
pln Смешав все реагенты, Петр получил достаточно большую кучу черного порошка.

inv+ Мешок с порохом
goto casdnwlab
end

:casdnwlabfire
pln Спичек в средневековой лаборатории не нашлось, зато Бейл обнаружил там огниво.
inv+ Огниво
goto casdnwlab
end

:casdnwlabcel
;pln 123321 взял
inv+ Селитра
goto casdnwlab
end

:castleupstairs
if count_castleupstairs=1 then pln На втором этаже Петр обнаружил множество закрытых дверей, единственное, что было не заперто это небольшая комната-кладовка с различными инструментами.
if count_castleupstairs>1 then pln Петр поднялся по лестнице и зашел в кладовку.

if count_casstore=0 and needkaboom=1 then btn casstore, Поискать ведро и лопату
btn castelyard, Спуститься
end

:casstore
pln Перерыв кучу хлама и подняв не меньшую тучу пыли, Петр отыскал все необходимое.
candig=1
inv+ Ведро
inv+ Лопата
goto castleupstairs
end

:columnrealboom
hide_use=1
music="Boom.mp3"
pause 2000

pln Едва Петр успел отбежать от замка, как прогремел взрыв, да так сильно, так сильно, что Бейл оглох на несколько минут. Видимо, часть реагентов в лаборатории были легковоспламеняющимися. Но, судя по поднявшемуся уровню, свою задачу, Петр, все-таки, выполнил. "Если мне сейчас скажут что моя принцесса в другом замке, то я ..." - вытирая пыль, и грязь с лица грозился он.
pause 450
music="S2KML.mp3"

btn princessroomfk1, Идти в комнату принцессы
end

:princessroomfk1
pln "Надеюсь, хоть принцессу не завалило" - с этими мыслями, герой начал пробираться сквозь завалы. Но к счастью, комната принцессы каким-то чудом уцелела, более того, взрывом снесло лишь запертую дверь, и теперь, герой мог беспрепятственно войти, и взять свой трофей.
pln Пробравшись сквозь завал, Бейл, наконец-то, переступил порог комнаты королевской особы. Принцесса сидела, как в чем не бывало, на кровати, ожидая его.
pln Не совсем понимая, что именно он должен с ней делать, Петер подошел, и взял ее за руку. Принцесса, в свою очередь, встав, прильнула к его груди, и, улыбнувшись, прошептала:
pln - You haven't escaped you know.
btn princessroom, Далее
end

:princessroom
hide_use=1
pln Внезапно, мир померк, все краски исчезли, осталась лишь безграничная тьма.
pln - Эй, что за дела! Я ослеп! - в панике закричал Петр.
pln - Шлем сними, дубина, - ответил ему хор голосов.
pln Стянув шлем, он огляделся, вокруг него толпилась куча народу, а впереди стоял улыбающийся Сергей.
pln - С первым апреля тебя, - сказал он, протянув руку, - Яблоко будешь?
end






-------------------------------- МОСТ ----------------------------------------------
:minigamebridge
music="SKML.mp3"
hide_use=1

Play sword.mp3





pln <div align="center"><img src="Running.gif" class="foto" alt="" border=”0?></a></div>


btn mgbrigde1, Один
btn mgbrigde2, Два
btn mgbrigde3, Три
btn mgbrigde4, Четыре

mgbpress3=0
pause 2900
mgbpress3=1
pause 300
mgbpress3=2
pause 500
mgbrfailfail=1
goto FAILMBG
end


:mgbrigde3
if mgbpress3=2 then pln Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
if mgbpress3=0 then pln Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
mgbpress3=0

btn mgbrigde1, Один
btn mgbrigde2, Два
btn mgbrigde3, Три
btn mgbrigde4, Четыре

mgbpress1=0

Play push.mp3

pause 200


pause 150
mgbpress1=1
pause 200
mgbpress1=2
pause 250
mgbrfailfail=1
goto FAILMBG
end


:mgbrigde1
if mgbpress1=2 then pln Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
if mgbpress1=0 then pln Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
mgbpress1=0

btn mgbrigde1, Один
btn mgbrigde2, Два
btn mgbrigde3, Три
btn mgbrigde4, Четыре

mgbpress4=0

Play push.mp3
pause 200

pause 150
mgbpress4=1
pause 300
mgbpress4=2
pause 250
mgbrfailfail=1
goto FAILMBG
end


:mgbrigde4
if mgbpress4=2 then pln Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
if mgbpress4=0 then pln Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
mgbpress4=0

btn mgbrigde1, Один
btn mgbrigde2, Два
btn mgbrigde3, Три
btn mgbrigde4, Четыре

mgbpress2=0

Play push.mp3
pause 200

pause 150
mgbpress2=1
pause 250
mgbpress2=2
pause 250
mgbrfailfail=1
goto FAILMBG
end


:mgbrigde2
if mgbpress2=2 then pln Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
if mgbpress2=0 then pln Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
mgbpress2=0

btn finishmgb, Один
btn mgbrigde2, Два
btn mgbrigde3, Три
btn mgbrigde4, Четыре

mgbpressf=0

Play push.mp3
pause 200

pause 100
mgbpressf=1
pause 200
mgbpressf=2
pause 250
mgbrfailfail=1
goto FAILMBG
end

:finishmgb
if mgbpressf=1 then pln Едва Бейл шагнул на твердую землю, последний камень моста рухнул в воду подняв кучу брызг. & goto NOTFAILMBG
if mgbpressf=2 then pln НЕ УСПЕЛ Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
if mgbpressf=0 then pln РАНО Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку. & goto FAILMBG
mgbpress2=0

Play push.mp3
pause 200

end

:FAILMBG
if mgbrfailfail=1 then pln Замешкавшись Петр не успел перепрыгнуть падающую платформу и упал в реку.
mgbrfailfail=0
pln Вы проиграли.
btn minigamebridge, Начать заново
if count_FAILMBG>2 then btn rivebridgenew3fake, Пропустить мини-игру
end

:NOTFAILMBG
hide_use=0

btn rivebridgenew3fake, Далее
mgbgwontr=1
end
-------------------------------- МОСТ ----------------------------------------------







-------------------------------- ВОЛКИ ----------------------------------------------

:mgfwstart
music="ATKML.mp3"

image="fon.gif"

pln <a href="btn:fwmgbutt1"><img src="butt1.png" class="foto" style="position:absolute##59$left:-4px##59$top:131px" border="0"></a>
pln <a href="btn:fwmgbutt2"><img src="butt2.png" class="foto" style="position:absolute##59$left:248px##59$top:217px"  border="0" ></a>
pln <a href="btn:fwmgbutt3"><img src="butt2.png" class="foto" style="position:absolute##59$left:16px##59$top:20px" border="0" ></a>
pln <a href="btn:fwmgbutt4"><img src="butt1.png" class="foto" style="position:absolute##59$left:354px##59$top:195px"  border="0" ></a>
pln <a href="btn:fwmgbutt5"><img src="butt2.png" class="foto" style="position:absolute##59$left:255px##59$top:103px"  border="0" ></a>
pln <a href="btn:fwmgbutt6"><img src="butt1.png" class="foto" style="position:absolute##59$left:78px##59$top:122px"  border="0" ></a>

mgfwpress1=0
pause 1000
mgfwpress1=1
pause 800
mgfwpress1=2
pause 500
mgfwfailfail=1
goto FAILFWG


end

:fwmgbutt1
if mgfwpress1=2 then pln Петр не успел срегировать на яростный прыжок волка и упал ничком заливая кровью траву под собой. & goto FAILFWG
if mgfwpress1=0 then pln Взмахнув мечом Бейл вспорол лишь воздух, волки не простили ему эту ошибку, сбив его с ног перегрызли горло и убежали оставив его лежать в луже собственной крови. & goto FAILFWG
mgfwpress1=0

mgfwpress2=0

Play sword.mp3
pause 350

pause 450
mgfwpress2=1
pause 600
mgfwpress2=2
pause 500
mgfwfailfail=1
goto FAILFWG

end

:fwmgbutt2
if mgfwpress2=2 then pln Петр не успел срегировать на яростный прыжок волка и упал ничком заливая кровью траву под собой. & goto FAILFWG
if mgfwpress2=0 then pln Взмахнув мечом Бейл вспорол лишь воздух, волки не простили ему эту ошибку, сбив его с ног перегрызли горло и убежали оставив его лежать в луже собственной крови. & goto FAILFWG
mgfwpress2=0

mgfwpress3=0

Play sword.mp3
pause 350

pause 450
mgfwpress3=1
pause 600
mgfwpress3=2
pause 500
mgfwfailfail=1
goto FAILFWG
end

:fwmgbutt3
if mgfwpress3=2 then pln Петр не успел срегировать на яростный прыжок волка и упал ничком заливая кровью траву под собой. & goto FAILFWG
if mgfwpress3=0 then pln Взмахнув мечом Бейл вспорол лишь воздух, волки не простили ему эту ошибку, сбив его с ног перегрызли горло и убежали оставив его лежать в луже собственной крови. & goto FAILFWG
mgfwpress3=0

mgfwpress4=0

Play sword.mp3
pause 350

pause 450
mgfwpress4=1
pause 600
mgfwpress4=2
pause 500
mgfwfailfail=1
goto FAILFWG
end

:fwmgbutt4
if mgfwpress4=2 then pln Петр не успел срегировать на яростный прыжок волка и упал ничком заливая кровью траву под собой. & goto FAILFWG
if mgfwpress4=0 then pln Взмахнув мечом Бейл вспорол лишь воздух, волки не простили ему эту ошибку, сбив его с ног перегрызли горло и убежали оставив его лежать в луже собственной крови. & goto FAILFWG
mgfwpress4=0

mgfwpress5=0

Play sword.mp3
pause 350

pause 450
mgfwpress5=1
pause 600
mgfwpress5=2
pause 500
mgfwfailfail=1
goto FAILFWG
end

:fwmgbutt5
if mgfwpress5=2 then pln Петр не успел срегировать на яростный прыжок волка и упал ничком заливая кровью траву под собой. & goto FAILFWG
if mgfwpress5=0 then pln Взмахнув мечом Бейл вспорол лишь воздух, волки не простили ему эту ошибку, сбив его с ног перегрызли горло и убежали оставив его лежать в луже собственной крови. & goto FAILFWG
mgfwpress5=0

mgfwpress6=0

Play sword.mp3
pause 250

pause 550
mgfwpress6=1
pause 600
mgfwpress6=2
pause 500
mgfwfailfail=1
goto FAILFWG
end

:fwmgbutt6
if mgfwpress6=1 then pln Прикончив первого волка и пронзив второго насквозь, Петр отдышался. Осознав что уже несколько минут держит в руке рукоятку меча, он швырнул ее в сторону волка из которого торчал обломок лезвия. & goto forestendfake2
if mgfwpress6=2 then pln Петр не успел срегировать на яростный прыжок волка и упал ничком заливая кровью траву под собой. & goto FAILFWG
if mgfwpress6=0 then pln Взмахнув мечом Бейл вспорол лишь воздух, волки не простили ему эту ошибку, сбив его с ног перегрызли горло и убежали оставив его лежать в луже собственной крови. & goto FAILFWG
mgfwpress6=0

Play sword.mp3
pause 350

end

:FAILFWG

if mgfwfailfail=1 then pln Петр не заметил стремительную атаку зверя и волк повалив его на спину вцепился в горло.
mgfwfailfail=0
pln Вы проиграли.
btn mgfwstart, Попробовать еще раз
if count_FAILFWG>2 then btn forestendfake2, Пропустить мини-игру
end

-------------------------------- ВОЛКИ ----------------------------------------------








;-----------------------------РНД--------------------------------
:rnd()
число=до-от+1
proc rnd
рнд=рнд+от-1
end


:rnd
рнд=rnd*число+1
i=0
:rnd_loop
i=i+1
if рнд>i and рнд<(i+1) then рнд=i
if i<число then goto rnd_loop
end
;-----------------------------РНД--------------------------------


 