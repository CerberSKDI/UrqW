:0
clrscr=1
cls
invkill
perkill
coler=1
proc yellow
pln
pln
pln
pln
pln                          ВИННИ-ПУХ И ДРУГИЕ ЗВЕРИ...
pln
proc blue
pln                            Маленькая тестовая игра
pln                            для демонстрации работы
pln                             с инвентарем и цветом
proc white
pln
pln                                 Автор: Korwin
pln
proc violet
pln                       Для URQ_DOS от 28.07.2004 (и выше:)
pln
pln     *********************************************************************
; Начата 26.09.2003, закончена (послесловие и предисловие) 13.08.2004
; совместимость URQ_DOS 1.35  от 28.07.2004
Style_Dos_TextColor=12
pln                                Внимание!
pln                  В этой игре Ваш герой не может умереть.
proc violet
pln            Для работы с инвентарем служат клавиши I(инвентарь)
pln                     и U(от англ. use - использовать)
pln
proc yellow
common=1
proc yellow
Желудь_лок=6
Ягода_лок=22
Ромашка_лок=7
Style_Dos_ButtonColor=7
Style_Dos_CursorColor=116
btn 1,Играть
btn option,Настройки
btn ex,Выход
end

:ex
end

:option
proc violet
pln Имейте в виду, что сменить уровень сложности в середине игры нельзя!
p Текущий уровень:
proc white 
if level=0 then p  Сложный &proc violet&pln (У вас в голове ОПИЛКИ. #/$Прямо как у Винни-Пуха. Который счастлив!)
if level=1 then p  Средний &proc violet&pln (У вас в голове ЭРУНДИРОВАННЫЕ МОЗГИ. #/$Как у Совы. А ведь она умеет правильно писать слово "суббота"!)
if level=2 then p  Легкий &proc violet&pln (У вас в голове НАСТОЯЩИЕ МОЗГИ. #/$Как у Кролика. Который, как известно, умный, образованный и поэтому... ничего не понимает:)
btn levelplus,Сменить уровень
p Настройка цветов:#$
proc yellow
if coler=0 then pln Цвет серый, стандартный&btn coleron,Цвета автора else pln Цвета автора.#$&btn coleroff,Стандартный цвет 
btn 1,Играть
end

:levelplus
if level<2 then level=level+1 else level=0
goto option

:common
step=step+1
if spis<>0 then леший=1
end

:common1
end


:1
common=0
inv+ Винни-Пух
proc yellow
лок=6
pln В один прекрасный день, - скорее всего, это была пятница или суббота, ну может быть ещё четверг, но при этом точно не понедельник! - так вот, в один прекрасный день, Винни-Пух сидел под деревом, и настроение у него было тоже такое прекрасное, что опилки сами шевелились у него в голове. И от этого шевеления рождалась замечательная песня - бормоталка: & proc yellow
stih=2
p Шурум-бурум, шурум-бурум,#/$Идет-бредет ...
btn 3, медведь Пум-пум!& btn 4, Пятак-Тугодум!&btn 2,зеленый шум!
end

:2
pln  Это попахивает Тютчевым... Но Пух его не читал. Пойдет!&stih=1
btn 5,Дальше
end

:3
pln Отличное продолжение! Главное - про медведей!&stih=2
btn 5,Дальше
end

:4
pln Так ему Пятаку! Он, по правде, туговато соображает! С другой стороны, всё-таки друг...&stih=3
btn 5,Дальше
end

:5
proc green 
pln Пуху так понравилась новая песня, что ему очень захотелось залезть на дерево и спеть её с вершины, чтобы было далеко слышно! Или пойти к Пятачку и прочитать стихи ему. Или сотворить еще что-нибудь этакое! Вдруг откуда-то сверху Винни услышал короткий смешок...
btn 6,Встать и осмотреться
end

:6
лок=6
if Шишка_лок=9 then Шишка_лок=6
proc green
pln Медвежонок находится под Высоким-Превысоким Дубом. &if oak<3 then pln Он такой громадный, что его даже не охватить одним взглядом! &oak=oak+1
proc obj
btn 7,Идти на поляну
btn 9, Лезть наверх
btn 16, Отойти назад и осмотреть Дуб
if level=2 and bum=1 then btn bearforce,Потрясти дуб
if bush=1 then btn 22,Осмотреть куст ежевики
end

:7
лок=7
count12=0
леший=0
if poem=1 then btn 42,Придумать другие стихи
proc green
if Желудь_лок=12 or Желудь_лок=13 or Желудь_лок=14 then Желудь_лок=6
if Ромашка_лок=12 or Ромашка_лок=13 or Ромашка_лок=14 then Шишка_лок=14&Ромашка_лок=7
if polana<=3  then p Косолапки привели медвежонка на чудесную зелено&proc white&p -бело-&proc yellow&p желтую &proc green&p полянку, всю покрытую ромашками. От запаха цветов воздух такой вкусный, что кажется, его можно пить! &polana=polana+1
if count_7>2 then proc white&p Ромашковая &proc green&p поляна. Красиво...#$
proc obj
btn 6,Идти к Дубу
btn 8, Идти к Дому Пятачка
btn 12,Идти к Трём Соснам
end

:8
лок=8
hello=0
proc yellow
p На поляне перед домом Пятачка.#$
if osmhouse=0 then proc 39
pln
if Пятачок=0 then btn 24, Постучать в дверь
if Пятачок=1 then p Из дверей вышел поросёнок - друг медвежонка.#$&btn 26, Осмотреть Пятачка&btn 27, Поговорить с Пятачком
proc obj
btn 7,Идти на поляну
end

:9
лок=9
proc green
if Ружье then p Пух не может лезть по дереву с ружьем в лапах! Его придется оставить!#$&goto 6
if vorona=0 then proc red&p Внезапно на Пуха налетела огромная черная птица и тюкнула его клювом по голове! Бум! Бедный медвежонок полетел вниз! #/$&proc green&bum=1&goto 6
pln Пух влез по стволу Дуба до большой ветки. &proc obj
if vorona=1 then p Злобная Ворона сидит на верхней ветке и пристально смотрит на Пуха. Взгляд у неё - нехороший...&if Шишка then btn 23, Запустить в Ворону шишкой!
btn 6,Спуститься вниз
btn 10, Залезть ещё выше!
btn 11, Осмотреть дупло
end

:10
лок=10
if Мед_лок=0 and not Мед then Мед_лок=10
proc green
if vorona=1 then proc red&p Внезапно на Пуха налетела мстительная ворона и тюкнула его клювом по голове! Бум! Винни с жалобным воплем полетел, кувыркаясь, с вершины! Довольная Ворона снова перелетела на нижнюю ветку!#/$&proc green&vorona=0&bum=1&goto 22
p Пух залез на самую вершину Высокого-Превысокого Дуба! Здесь есть &proc yellow&p гнездо лесных пчёл! &proc green
proc obj
btn 9,Спуститься чуть пониже
btn 25,Осмотреться вокруг
end

:11
лок=11
proc violet
if vorona=1 then proc red&p Внезапно на Пуха налетела огромная черная птица и тюкнула его клювом по голове! Бум! Бедный медвежонок полетел вниз! &proc violet&bum=1&goto 22
pln В этом дупле ворона устроила своё гнездо! Здесь лежит всякий хлам, собранный жадной птицей по всему лесу.&if Значок_лок=0 and not Значок and presentsign=0 then Значок_лок=11
proc obj
btn 9, Вылезти из дупла
end

:12
лок=12
counter=0
proc green
if count_12=1 then pln Это священные Три Сосны! Они заколдованы, но никто не знает почему...Говорят, под одной из них живет хитрый лешачок...
pln Пух подошел к высокой сосне. Она точно такая же, как другие сосны в этом зачарованном месте: одна слева, другая справа.
p Вокруг сосен колышется странный белесый туман.#$
proc obj
btn 13,Идти к левой сосне
btn 14,Идти к правой сосне
btn 7, Идти на поляну
btn 48,Разведать что в тумане
end

:13
лок=13
proc green
counter=counter+1
pln Пух подошел к высокой сосне. Она точно такая же, как другие сосны в этом зачарованном месте: одна слева, другая справа. 
if леший=1 and rnd4=1 and vorona<>2 then p Пух замечает, что за соседней сосной мелькнула тень, но затем, как будто испугавшись чего-то, она метнулась прочь, в туман.#$
if vorona=2 then p Впрочем, нет! На этой сосне сидит знакомая ворона!&btn Vorona,Поговорить с вороной
p Вокруг сосен колышется странный белесый туман.#$
proc green
if counter=3 then p Странно! Сосна выглядит очень знакомой!
if counter=5 then p Кажется, Пух уже был здесь!
if counter=7 then p Ужасно! Винни определенно заблудился!
if counter=9 then p Сколько же можно блуждать!
proc obj
if osmotr=1 then btn 48, Идти к морю! По крайней мере, в ту сторону...
if osmotr=1 and Компас and direct=2 then btn 40, Идти на юг!
if Компас and direct>0 then btn 12,Идти на север
if леший=0 and vorona<>2 then btn 14,Идти к левой сосне
if леший=1 or vorona=2 then btn 12,Идти к левой сосне
btn 14,Идти к правой сосне
btn 48,Разведать что в тумане
end

:14
лок=14
proc green
counter=counter+1
pln Пух подошел к высокой сосне. Она точно такая же, как другие сосны в этом зачарованном месте: одна слева, другая справа. 
if vorona=2 then p Впрочем, нет! На соседней сосне сидит знакомая ворона!&btn 13,Подойти поближе к Вороне
if леший=1 and rnd5=1 and vorona<>2 then p Пух замечает, что за соседней сосной мелькнула тень, но затем, как будто испугавшись чего-то, она метнулась прочь, в туман.#$
p Вокруг сосен колышется странный белесый туман.#$
if counter=4 then p Сосна выглядит очень знакомой!
if counter=6 then p Определенно, Пух уже был здесь!
if counter=8 then p Ужасно! Винни заблудился!
if counter=10 then p Устал! Ну сколько же можно блуждать!
proc obj
if osmotr=1 then btn 48, Идти к морю! По крайней мере, в ту сторону...
if osmotr=1 and Компас and direct=2 then btn 40, Идти на юг!
if Компас and direct>0 then btn 12,Идти на север
btn 13,Идти к левой сосне
if леший=0 and vorona<>2 then btn 13,Идти к правой сосне
if леший=1 or vorona=2 then btn 12,Идти к правой сосне
if counter>10 then p Измученный блужданиями между соснами, Пух решил бежать прочь&btn 15, Бежа-а-ать!!!
btn 48,Разведать что в тумане
end

:15
proc green
pln Пух с воплем кинулся бежать прочь, запнулся о корень и, падая, с размаху стукнулся лбом о ствол!#/$&proc red&pln      БАЦ!!!#/$&proc green&pln Все мысли разом вылетели из набитой опилками головы!
p Пух сел. Вокруг головы явственно летали маленькие &proc white&p птички.#$ &proc green&p Потом улетели...Медвежонок пришел в себя.
btn 12, Оглядеться
end


:16
лок=16
proc green
p Пух видит величественный Дуб, вершина которого, кажется, достает до самого неба. 
proc obj
if vorona=0 then p На ветке сидит Ворона и смотрит на плюшевого медвежонка.&btn 17, Поговорить с Вороной&if Ружье then btn 18, Выстрелить в Ворону
if vorona=0 and Желудь then btn 46, Желудем Вороне в глаз!
if vorona=0 and Шишка then btn 47, Запустить в Ворону Шишкой!
btn 6, Подойти к Дубу
end

:17
proc green
if vorona=0 then pln Ворона насмешливо смотрит на Пуха. Что ей сказать?&btn 19,Привет!&btn 20,Пожалуйста, разреши мне залезть на дуб.&btn 21,Хочешь получить по хохотальнику?
if vorona<>0 then pln Ворона улетела. Больше говорить не с кем...
btn 16,Закончить разговор
end

:18
proc green
if заряд=0 then pln Ружье не заряжено!&goto 16
p Пух прицелился и выстрелил! Желудь больно стукнул Ворону по башке, она свалилась с ветки, но вовремя распахнула крылья и, возмущенно каркнув:"Карраул! Убивают!", - перелетела повыше...#$&заряд=0&vorona=1&Желудь_лок=6&shot=1&goto 6
end

:19
if shot=0 then pln - Прривет, Дуррашка! - отвечает Ворона.
if shot=1 and not Ружье then pln - Сам, Дуррак! - злобно отвечает Ворона.
if shot=1 and Ружье then pln - Прривет! - отвечает Ворона, с опаской поглядывая на ружье.
btn 17, Продолжить разговор
end

:20
if shot=0 then pln - Ха-ха! - отвечает веселая птица.
if shot=1 and not Ружье then pln - Тррус! - каркает Ворона.
if shot=1 and Ружье then pln - Попрробуй! - нагло говорит Ворона, но на всякий случай передвигается в сторону, стараясь спрятаться между дубовыми листьями. 
btn 17, Продолжить разговор
end

:21
if shot=0 then pln - Дуррак!!! - комментирует Ворона Пухову мысль.
if shot=1 and not Ружье then pln - Разбойник! Воррр! - каркает Ворона.
if shot=1 and Ружье then pln - Карраул! - каркает Ворона, и улетает на верхнюю ветку.&vorona=1
btn 17, Продолжить разговор
end

:22
лок=22
proc green
pln Пух оказался в кустах ежевики и набрал полные лапы колючек. &proc obj&bush=1
btn 6,Вылезти из кустов
end

:23
pln Пух прицелился и метнул свое грозное оружие в Ворону! Птица увернулась, но, наученная горьким опытом - этот медведь рано или поздно попадет! - улетела прочь. &vorona=2&inv- Шишка&Шишка_лок=22 
goto 9
end

:24
if direct=2 then p Никто не отозвался. Возможно, Пятачок ушел в гости к Кролику. Или так увлекся своими значками, что ничего уже не слышит...#$
if свинсердит>0 and direct<2 then pln - Чего ещё? - доносится из-за двери...Потом дверь открывается и выходит угрюмый Пятачок.#$&Пятачок=1
if свинсердит<=0 and direct<2 then pln - Кто там? - раздается приветливый голосок. Потом дверь распахивается и на порог выскакивает Пятачок.#$&Пятачок=1
btn 8, Дальше
end

:25
pln С высоты Пух увидел всё: и маленький домик Пятачка, и 
p зелено&proc white&p -бело-&proc yellow&p желтую&proc green&p #$Ромашковую полянку, и Три Сосны...#/$
pln А за соснами, на полдень от них - #/$&proc darkblue&pln        СИНЕЕТ МОРЕ!#/$&osmotr=1
proc green
pause 2000
p Внезапно Пуха укусила коварная пчела!&proc obj
btn 10, Ой!!!
end

:26
if свинсердит<-5 then pln Пятачок - лучший друг Винни-Пуха!
if свинсердит=-5 then pln Пятачок выглядит просто счастливым!
if свинсердит=-4 then pln Пятачок в прекрасном настроении!
if свинсердит=-3 then pln Поросенок вполне доволен!
if свинсердит=-2 then pln Пятачок улыбается. У него, оказывается, очень симпатичные ушки!
if свинсердит=-1 then pln Пятачок приветливо смотрит на Пуха.
if свинсердит=0 then pln Пятачок смотрит на Винни-Пуха. Он выглядит озабоченным.
if свинсердит=1 then pln Пятачок выглядит обиженным. Чего это с ним?
if свинсердит=2 then pln Пятачок явно расстроен. Что это с ним?
if свинсердит>2 then pln Пятачок в гневе! Совсем освинел, что ли?
btn #%previous_loc$, Дальше
end

:osmpyatak
if свинсердит<-5 then pln Пятачок - лучший друг Винни-Пуха!
if свинсердит=-5 then pln Пятачок выглядит просто счастливым!
if свинсердит=-4 then pln Пятачок в прекрасном настроении!
if свинсердит=-3 then pln Поросенок вполне доволен!
if свинсердит=-2 then pln Пятачок улыбается. У него, оказывается, очень симпатичные ушки!
if свинсердит=-1 then pln Пятачок приветливо смотрит на Пуха
if свинсердит=0 then pln Пятачок смотрит на Винни-Пуха. Он выглядит озабоченным.
if свинсердит=1 then pln Пятачок выглядит обиженным. Чего это с ним?
if свинсердит=2 then pln Пятачок явно расстроен. Что это с ним?
if свинсердит>2 then pln Пятачок в гневе! Совсем освинел, что ли?
end


:27
if level>0 then proc osmpyatak
pln Что плюшевый медведь скажет поросенку?
btn 28, У тебя есть что-нибудь вкусненькое?
if sign=0 then btn 29, Что случилось, Пятачок?
if hello=0 then btn 30, Привет, старина!&btn 30, Здравствуй, Пятачок!&btn 31, Как дела, свинтус?
if свинсердит>0 and sorry=0 then btn 50, Извини, пожалуйста, Пятачок. Я был не прав!
if poem=0 then btn 32, Пятачок, я специально для тебя придумал классные стихи!&btn 32, Хочешь, я прочитаю для тебя свои новые стихи, Пятачок?
if pogoda=0 then btn 33, Сегодня отличная погода чтобы погулять!
if Значок or Ромашка or Ягода or Шишка or Желудь then btn 34, Пятачок, у меня есть для тебя Подарок!
if bum=1 and not Ружье and dulo=0 then btn 35, Пятачок,  ты не мог бы одолжить мне свое ружье?
if count_12>1 and pak=0 then btn 38, Послушай, Пятачок! Мне нужна твоя помощь! Ты что-нибудь знаешь о Трёх Соснах?
if Компас and  свинсердит<0 then btn talksea,Пятачок, а что это за штука...КОМПАС?
if Ружье then btn ОтдатьРужье,Спасибо, Пятачок! Возьми свое Ружье назад.
if hello>0 then btn 37, Покеда, маленькая грязная свинья!&btn 36, До свидания, друг!
if level=0 then btn 26, Осмотреть Пятачка.
hello=1
end

:28
if blackberry=0 then pln - Нет, извини Пух! Ты всё съел в прошлый раз!&свинсердит=свинсердит+1 
if blackberry=1 and givhoney=1 and свинсердит<0 then pln - Прости, Пух, у меня действительно, ничего больше нет...
if blackberry=1 and givhoney=0 and свинсердит>=0 then pln - Пух, знаешь, у меня ... ничего нет. Нет! Ты все съел! Еще в прошлый раз!&свинсердит=свинсердит+1
if blackberry=1 and givhoney=0 and свинсердит<0 then pln - Знаешь, Пух, у меня тут есть немного сотового меда. Возьми, пожалуйста! &inv+ Мед&givhoney=1&свинсердит=свинсердит+1
btn 27, Продолжить разговор 
end

:29
pln - Я потерял любимый значок из своей коллекции! - печально говорит Пятачок. &свинсердит=свинсердит-1&sign=1
btn 27, Продолжить разговор 
end

:30
if howdo=2 then p - Другое дело!#$&howdo=0&свинсердит=свинсердит-1
if howdo=1 then p - Здоровались уже! Но все равно - #$&свинсердит=свинсердит+1&howdo=0
if howdo=0 then pln - Здравствуй, Пух! - отвечает поросенок, улыбаясь...&hello=1&свинсердит=свинсердит-1&howdo=1
btn 27, Продолжить разговор 
end

:31
pln Пятачок насупился.&свинсердит=свинсердит+1&hello=1&howdo=2
btn 27, Продолжить разговор
end

:32
свинсердит=свинсердит-1&poem=1
pln - Конечно, прочитай их мне, Пух! - оживился Пятачок. И Пух торжественно читает:#/$&proc blue
if stih=1  then st1=st1+1&proc stih1
if stih=2  then st2=st2+1&proc stih2
if stih=3  then st3=st3+1&proc stih3
if stih=1 and st1>=2 then p - Но ты уже читал их мне. - вежливо замечает Пятачок.&свинсердит=свинсердит+1
if stih=2 and st2>=2 then p - Пух, ты здоров? Ты снова читаешь те же самые стихи! - озабоченно говорит Пятачок.&свинсердит=свинсердит+2
if stih=3 and st3>=2 then p - Ты издеваешься?!. - возмущенно спрашивает Пятачок.&свинсердит=свинсердит+3
proc yellow
btn 27, Продолжить разговор 
end
:33
if presentsign=1 then pln - Отличная идея, Пух! Пойду-ка я прогуляюсь до Кролика!
if presentsign=0 then pln - Мне некогда гулять, - говорит поросенок. - Я должен искать свою пропажу.
btn 27, Продолжить разговор 
end

:34
pln Что Винни предложит Пятачку в подарок?
if Желудь then btn ДарЖелудь, Вкусный Желудь
if Значок then btn ДарЗначок, Серебряный Значок
if Ромашка then btn ДарРомашка, Прекрасный Цветок
if Шишка then btn ДарШишка, Большую Шишку
if Ягода then btn ДарЯгода, Лесную Ежевику
btn 27,Я забыл твой подарок дома, Пятачок!
end

:35
if свинсердит>-5 then pln - Пух, это очень дорогое Ружье! Я не могу тебе его дать! - говорит Пятачок.
if свинсердит<-4 then pln - Хорошо, я дам тебе Моё Ружье! - торжественно говорит Пятачок. &inv+ Ружье&dulo=1&свинсердит=свинсердит+1
btn 27, Продолжить разговор 
end
:36
pln - До свидания! - говорит Пятачок и уходит в свой домик. Дверь закрывается. &Пятачок=0
btn 8, Пока!
end

:37
pln Пятачок в гневе захлапывает дверь! &свинсердит=свинсердит+2&Пятачок=0
btn 8, А пропади оно!
end

:38
if count_38=1 then pln - Я слышал, - говорит поросенок, - что в Трех соснах живет леший. Он кружит и кружит путников между сосен, пока они не падают от усталости и не сходят с ума. Поэтому Его никто не любит и не дарит Ему подарков...Никогда-никогда!#$&свинсердит=свинсердит-1
if count_38=2 then pln - Тот лешачок, который живет в Трёх соснах, очень застенчивый, и никому не показывается на глаза...
if count_38>2 then pln - Больше я ничего не знаю про Три сосны.- говорит Пятачок. Кажется, ему уже надоела эта тема. &свинсердит=свинсердит+1&pak=1
btn 27, Продолжить разговор 
end

:39
p На опушке леса стоит изящный двухэтажный домик, с маленькой дверцей (куда Пуху при всём желании не протиснуться) и вывеской над входом "Посторонним В"#$&osmhouse=1
end

:40
лок=40
proc yellow
p Пух стоит на песчаном берегу, куда с шумом накатываются &proc darkblue&p изумрудно-синие волны!#$&proc yellow&proc obj&p #$
if камень=0 then p Еще на берегу стоит большой &proc white&pln Белый Камень.#$&btn 49,Повернуть Камень
if камень=1 then proc yellow&pln В ямке из-под камня Пух видит удивительно красивую, прозрачную гальку цвета мёда!&btn 41,Взять Янтарь
btn 12, Вернуться к Трем соснам 
end

:41
cls
proc yellow
pln Пух подбирает кусочек Янтаря, и видит в нем Большого Золотого Жука. Винни внимательно смотрит на Жука, а Тот...смотрит на Винни! 
pause 4000
p И вдруг начинает ШЕВЕЛИТЬСЯ, потом ВЫСОВЫВАЕТ СТРАШНЫЕ ЗУБАСТЫЕ ЖВАЛЫ из Янтаря и хватает ими Пуха за нос! В ужасе Пух кричит:#$&btn Проснуться, Караул!!!
end

:42
proc violet
poem=0
pln Пух старательно придумывает новую бормоталку: & proc yellow
p Шурум-бурум, шурум-бурум,#/$Идет-бредет ...
btn 44, медведь Пум-пум!&btn 43,зеленый шум!&btn 45, Пятак-Тугодум!
end

:43
pln  Это попахивает Тютчевым... Но Пятачок его не читал. Пойдет!&stih=1
btn 7,Дальше
end

:44
pln Отличное продолжение! Главное - про медведей!&stih=2
btn 7,Дальше
end

:45
pln Так ему Пятаку! Он по правде туговато соображает! С другой стороны, всё-таки друг...&stih=3
btn 7,Дальше
end

:46
proc red
pln Медведь взял желудь, примерился. Метнул! 
pln Эх, легковато оружие. Не долетел желудь до наглой птицы! 
pln Ворона с опаской посмотрела вниз и сказала:"Дуррак!"
inv- Желудь&Желудь_лок=6
proc green
goto 16

:47
proc red
count47=count47+1
if count47<3 then pln Пух подкинул на лапе шишку - увесистая! - и с силой швырнул в Ворону!  Еле-еле вредная птица увернулась! Отскочила в сторону, села на прежнее место и снова громко сказала обидное слово:"Дуррак!" &inv- Шишка&Шишка_лок=6&proc green&goto 16
if (count47>=3 and shot=1) or (count47>=1 and level>0) then p Ага! Пристрелялся! Попал! Прямо по башке!#$
if shot=1 then p Туда же куда попадал из ружья - Шишкой по шишке!#$
pln Ворона свалилась с Дуба, но не долетев до земли, каркнула, развернула крылья, и, как-то боком улетела прочь!#$&inv- Шишка&Шишка_лок=6&vorona=2&proc green&goto 16

:48
proc grey
pln Пух долго блуждал в тумане...
pause 2000
pln Очень долго блуждал в тумане...
pause 2000
pln Совершенно промок и озяб в этом тумане...
pause 2000
proc blue
pln И, наконец, вышел из проклятого сырого тумана на свет!
proc green
btn 13,Оглядеться
end

:49
proc yellow
if strong<3 and level<2 then p Как Пух не пыхтел, и не бухтел, загадочный белый камень отвалить не удалось! Видно, мало каши ел! Или вдоволь меду съесть не пришлось?#$&pause 3000&goto 40
p Пух поднапрягся и, - О, чудо! - медвежьи мускулы, напоённые силой солнца, заключенной в мёде (а может быть, в лесных ягодах?), преодолели сопротивление камня. Камень упал, а под ним что-то сверкнуло!&камень=1&pause 3000&goto 40

:50
if level=0 then sorry=1
pln Потупя взор и пряча виноватые глаза, Пух произнес слова извинения. Пятачок вздохнул и, помолчав, ответил: Ладно, Пух! Забыто...&свинсердит=0
btn 27, Продолжить разговор
end

:talksea
if свинсердит>=0 then pln Извини, Пух, мне казалось ТЫ знаешь что это за штука...
if dulo=1 and свинсердит<0 then pln Пятачок задумчиво говорит: Хм-хм! Ну, компас это такая штука! С ним можно идти куда он показывает. Например, на юг... Я всегда мечтал сходить в южный лес и там... пострелять по шишкам. - он многозначительно смотрит на Пуха.&direct=1
if dulo=0 and свинсердит<0 then pln Довольный Пятачок задумчиво говорит: Хм-хм! Ну, компас это такая штука! С ним можно идти куда он показывает. Например, на север, к морю. Всегда мечтал найти кусочек янтаря на морском берегу!&direct=2
btn 27, Продолжить разговор
end

:stih1
pln Шурум-бурум, шурум-бурум!
pln Идет-гудет зеленый шум!
pln Шумят травинки и листы - 
pln Идем по лесу Я и Ты!
pln
pln Поёт в вершинах ветер -
pln Что жить легко на свете!
pln И сердцем ветру вторя,
pln Пойдем с Тобою к морю!
pln
pln Хотим мы лапы омочить
pln В береговой Волне!
pln Чтоб интересно стало жить -
pln Тебе и мне!
pln
pln Восхищенно помолчав, Пятачок говорит: Классные стихи! &свинсердит=свинсердит-2
end

:stih2
pln Шурум-бурум, шурум-бурум!
pln Идет по лесу Топотун!
pln Дрожат деревья и цветы,
pln И волки прячутся в кусты!
pln 
pln Я круче всех лесных зверей!
pln Я Дуб сломать могу!
pln Я Дом оставлю без дверей,
pln Без кочерги - Ягу!
pln 
pln Я выпью Море и Реку!
pln Чтоб сухо было впредь...
pln Я Горы растоптать могу -
pln Ведь Я - крутой Медведь!
pln 
pln Это Очень Громкие Стихи! - замечает Пятачок.#$
end

:stih3
pln Шурум-бурум, шурум-бурум!
pln Идет по лесу Тугодум!
pln Я не скажу, что он Дурак -
pln Он просто думает Не Так!
pln 
pln Его спроси про "Дважды Два"
pln Ответит к вечеру едва...
pln Спроси Его "Который Час?"
pln Лишь через час ответ он даст!
pln 
pln Скажи "А как тебя зовут?"
pln Ответа ждать - напрасный труд!
pln Но Я скажу без всяких врак:
pln Он Глупый Маленький Толстяк,
pln Его зовут - Кабан Пятак!
pln
pln Пятачок весь покраснел от обиды. На его глаза навернулись слезы..&свинсердит=свинсердит+3
end

:ОтдатьРужье
pln Пух протянул ружье поросенку. Тот взял его, незаметно осмотрел, улыбнулся и отнес в дом. Вернулся крайне довольный.&inv- Ружье&свинсердит=свинсердит-1&Желудь_лок=6&dulo=0
btn 27, Продолжить разговор 
end

:ДарЗначок
pln Пятачок с восторгом смотрит на медвежонка. Ты нашел мой Значок! Пух, можно Я тоже сделаю тебе подарок? Возьми вот этот компас!#$&&inv- Значок&Значок_лок=0&&свинсердит=-1&inv+ Компас&presentsign=1
btn 27, Продолжить разговор 
end

:ДарЖелудь
pln Пятачок с радостью принимает Желудь и съедает его.#$&&inv- Желудь&Желудь_лок=6&свинсердит=свинсердит-1
btn 27, Продолжить разговор 
end

:ДарРомашка
pln Пятачок берет Ромашку с удивлением: "Ты сорвал мою любимую Ромашку!" - грустно замечает он. - "Я любовался на Неё каждое утро..."#$&&inv- Ромашка&Ромашка_лок=7&свинсердит=свинсердит+1
btn 27, Продолжить разговор 
end

:ДарШишка
pln Пятачок  с удивлением смотрит на Шишку. Зачем мне Шишка? Оставь лучше себе, Пух!#$
btn 27, Продолжить разговор 
end

:ДарЯгода
pln Пятачок сказал "спасибо" и съел Ежевичку.#$&&inv- Ягода&Ягода_лок=22&свинсердит=свинсердит-1&blackberry=1
btn 27, Продолжить разговор 
end

:coleron
coler=1
Style_DOS_ButtonColor=7
Style_DOS_CursorColor=16*7
goto option

:coleroff
coler=0
Style_DOS_ButtonColor=7
Style_DOS_CursorColor=16*7
goto option

:grey
if coler=1 then Style_DOS_TextColor=7 else Style_DOS_TextColor=7
end

:darkblue
if coler=1 then Style_DOS_TextColor=9 else Style_DOS_TextColor=7
end

:green
if coler=1 then Style_DOS_TextColor=10 else Style_DOS_TextColor=7
end

:blue
if coler=1 then Style_DOS_TextColor=11 else Style_DOS_TextColor=7
end

:red
if coler=1 then Style_DOS_TextColor=12 else Style_DOS_TextColor=7
end

:violet
if coler=1 then Style_DOS_TextColor=13 else Style_DOS_TextColor=7
end

:yellow
if coler=1 then Style_DOS_TextColor=14 else Style_DOS_TextColor=7
end

:white
if coler=1 then Style_DOS_TextColor=15 else Style_DOS_TextColor=7
end


:obj
spis=0
instr vw=#$Пух видит#$
if Ружье_лок=лок then instr vw=#%vw$ ружье,#$&btn ВзятьРужье,Взять Ружье&spis=spis+1
if Желудь_лок=лок then instr vw=#%vw$ желудь,#$&btn ВзятьЖелудь,Взять Желудь&spis=spis+1
if Мед_лок=лок then instr vw=#%vw$ мёд,#$&btn ВзятьМед,Взять Мёд&spis=spis+1
if Ромашка_лок=лок then instr vw=#%vw$ цветок,#$&btn ВзятьРомашку,Взять Цветок&spis=spis+1
if Шишка_лок=лок then instr vw=#%vw$ шишку,#$&btn ВзятьШишку,Взять Шишку&spis=spis+1
if Значок_лок=лок then instr vw=#%vw$ значок,#$&btn ВзятьЗначок,Взять Значок&spis=spis+1
if Ягода_лок=лок then instr vw=#%vw$ ежевичку,#$&btn ВзятьЯгоду,Взять Ежевичку&spis=spis+1
if spis>0 then p #%vw$ и это, пожалуй, интересно для медвежонка.
end

:ВзятьЗначок
pln Пух подобрал металлический значок. Его можно кому-нибудь подарить!!#$&inv+ Значок&Значок_лок=0
btn #лок$, Назад
end

:ВзятьЯгоду
if лок=22 then pln Пух сорвал крупную черную ягодку!#$&inv+ Ягода&Ягода_лок=0
if лок<>22 then pln Пух снова подобрал ежевичку.#$&inv+ Ягода&Ягода_лок=0
btn #лок$, Назад
end

:ВзятьШишку
pln Медведь подумал - и взял шишку. Сгодится!#$&inv+ Шишка&Шишка_лок=0
btn #лок$, Назад
end

:ВзятьРомашку
pln Винни-Пух понюхал цветок - пахнет медом! И взял себе!#$&inv+ Ромашка&Ромашка_лок=0
btn #лок$, Назад
end

:ВзятьРужье
pln Винни взял ружье.#$&inv+ Ружье&Ружье_лок=0
btn #лок$, Назад
end

:ВзятьЖелудь
pln Винни берет твердый дубовый Желудь.#$&inv+ Желудь&Желудь_лок=0
btn #лок$, Назад
end

:ВзятьМед
pln Взял себе немного лесного меда!#$&inv+ Мед&Мед_лок=0
btn #лок$, Назад
end

:Use_Винни-Пух
pln Это же Я! Самый симпатичный, толстый и умный плюшевый медведь в лесу!
end

:Use_Винни-Пух_Поспать
pln Странно, но спать Пуху совсем не хочется!#$
end

:Use_Винни-Пух_Помощь
proc green
pln На самом деле здесь прячется маленькое предисловие автора игры, которое можно считать концептуальной помощью...
p Один маленький, но требовательный игрок (мой сын, когда ему было 8 лет) спросил, что здесь собственно надо делать. Для тех, кто задается таким же вопросом, - характерным, кстати для современных детей, испорченных компьютерными играми, -#$
proc white
p  я счёл возможным дать необходимые пояснения#$
proc green
p (эта фраза обязательно бы понравилась мудрой Сове).#$
p Цель игры - вполне русская: довести маленького мишку сначала до#$ 
proc yellow
p меда
proc green
p , затем до#$
proc darkblue
p моря
proc green
p , потом до полного#$
proc red
pln освинения:)
proc green
pln Тому, кто этими целями неудовлетворён, предлагаю найти более научные, например, сосчитать число всех возможных реплик вороны в беседе с Пухом, или более развлекательные, например выяснить не надоест ли когда-нибудь свинье жрать дармовые желуди!
pln Возможна даже постановка антропологических проблем ("О причинах, формах и развитии натурального товарообмена в первобытных обществах"), эстетическо-экологических ("Почему для Пятачка каждая Ромашка с поляны - любимая" или "О том, что будет, если цветы на Ромашковой поляне будут вырваны все до последнего") и даже правоведческих ("Возможно ли наказание гр. Вороны до совершения ею противоправного деяния в отношении гражданина В.Пуха несмотря на наличие мотива, замысла и возможностей совершения вышеуказанного деяния")
pln Заметьте, что самому Пуху вряд ли пришло бы в голову задавать подобные идиотские вопросы. Он просто живёт и наслаждается этим процесом. Ему-то с его характером, аппетитом и опилками в голове прохождение этого квеста было бы настолько легким, что он счёл бы это... приключением. 
pln
pln  Пользуясь случаем, приношу свою искреннюю и глубокую благодарность всем моим дорогим бета-тестерам, благодаря чьим советам игра стала значительно лучше и интереснее: Александру Шумову, Стасу Старкову, Виктору Корянову.
pln
pln Итак, будьте легкими, как Пух! С уважением,
proc violet
pln #$Korwin
proc green
pln
pln
end


:Use_Винни-Пух_Сохраниться
pln Пух решил сохранить себя для потомков - маленьких плюшевых медвежат!#$
curcolor=#Style_DOS_TextColor$
proc violet
save #%current_loc$
Style_DOS_TextColor=#curcolor$
end



:Use_Компас
pln Это очень красивый и полезный предмет! С ним даже в тумане не заблудишься! Жаль, что стрелку нельзя повернуть куда хочешь и приходится идти только туда, куда она показывает.#$
end

:Use_Ромашка
p Это большая красивая Ромашка!#$
end

:Use_Ромашка_Положить
p Пух бережно положил красивый цветок.#$&inv- Ромашка&Ромашка_лок=лок
end

:Use_Ромашка_Понюхать
p Ах! Как приятно пахнет! Даже голова кружится!#$
end

:Use_Желудь
p Обыкновенный спелый желудь.#$
end

:Use_Желудь_Бросить
p Медвежонок бросил желудь.#$&inv- Желудь&Желудь_лок=лок
end

:Use_Мед
p Этот лесной мёд замечательно пахнет! А цвет... непередаваемо красивый! Медовый...#$
end

:Use_Мед_Съесть
if strong<5 then p Съел немного меда. Вку-у-усно!#$&inv- 1,Мед&Мед_лок=0&strong=strong+1
if strong>=5 then p Пух уже не хочет меда!!! Объелся.
end

:Use_Мед_Положить
p Это глупо! Если мёд куда-нибудь положить, - кроме как в рот, - он вскоре исчезнет! Это вторая аксиома мёда, открытая мудрым Винни! Пух не будет этого делать!#$
end

:Use_Ружье
if заряд=0 then pln Это замечательное духовое ружье Пятачка. Жаль, оно не заряжено.#$
if заряд=1 then pln Осторожно! Ружье заряжено!#$
end

:Use_Ружье_Положить
p Пух положил ружье.#$&inv- Ружье&Ружье_лок=лок
end

:Use_Ружье_Зарядить
if заряд=1 then p Ружье уже заряжено!#$
if not Желудь then p А нечем заряжать!#$
if Желудь then inv- Желудь&заряд=1&p Пух затолкал желудь в дуло и взвел курок.#$
end

:Use_Шишка
pln Это необыкновенно большая и красивая сосновая Шишка! Наверное, это ответный подарок Лешего!#$
end

:Use_Шишка_Выбросить
p Пух выбросил шишку... Жалко! Такая была красивая шишка! Замечательная Шишка! Была...#$&inv- Шишка&Шишка_лок=лок
end

:Use_Значок
pln Это маленький блестящий значок в виде серебряного парусника.#$
end

:Use_Значок_Положить
p Пух выложил значок...Может, подберет кто-то, кому он нужнее.#$&inv- Значок&Значок_лок=лок
end

:Use_Ягода
pln Что её рассматривать - крупная красивая ежевичка! Её есть надо!#$
end

:Use_Ягода_Положить
pln С сожалением Пух аккуратно положил даже не облизанную ежевичку.#$&inv- Ягода&Ягода_лок=лок
end

:Use_Ягода_Съесть
if strong<5 then pln Медвежонок положил аппетитную сочную ежевичку в рот и с наслаждением раздавил языком... Объедение!#$&inv- Ягода&Ягода_лок=22&strong=strong+0.5
if strong>=5 then pln Пух сыт и не хочет больше есть.
end

:Vorona
proc green
pln - Привет, Ворона! - весело сказал Пух.
pln - Отстань, Врраг! - каркает замученная птица, и, хлопая крыльями, улетает прочь!#$&vorona=0
btn #лок$,Назад
end

:bearforce
pln Пух потряс дуб изо всех сил. Могучее дерево зашаталось. Сверху посыпались желуди, сухие листья, какой-то сор, и всякая мелочь из вороньего гнезда.
if vorona<2 then pln Перепуганная насмерть Ворона в панике улетела прочь, каркая на весь лес: "КАРРАУЛ! МЕДВЕДЬ-ШАТУН ИДЕТ!"&vorona=2
btn 6,Отряхнуть с себя мусор
end

:Проснуться
proc grey
cls
p Пух проснулся чудесным утром, когда розово-золотистые Солнечные Зайчики игриво залезают в окно, чтобы попрыгать на стене, а ранняя пташка Воробей уже поет свою задорную песню. Винни открыл глаза и осторожно потрогал нос - нос был в порядке. Винни огляделся вокруг. Он действительно лежал на своей кровати, под стареньким лоскутным одеялом, в своем уютном Домике. В окно светило солнце - вчера медвежонок забыл закрыть ставни... И тогда он подумал: Это был Удивительный&proc white&p #$Сон!#$&proc grey&p Страшный, но интересный! Точнее интересный, но немножко страшный. Его надо поскорее заесть! И запить!#$
anykey k
cls
pln
p Пух сделал то, что хотел, и его настроение снова стало#$ &proc yellow&p солнечно-медовым!&proc blue&p #$А небо голубым!#$&proc yellow&p #$И плюшевый медвежонок отправился гулять к своему лучшему другу Пятачку! И они ГУЛЯЛИ-ГУЛЯЛИ и пришли к Кролику, и там было так много "меда и сгущенного молока", что  Винни-Пух  объелся до #$&proc red&p ПОЛНОГО ОСВИНЕНИЯ!#$&proc yellow&pln И потом долго не мог выбраться из узкой двери. Но это уже совсем другая история...
anykey k
invkill
btn exit, КОНЕЦ
end

:exit
cls
proc white
pln Игра пройдена. Пожалуйста, введите Ваше имя для подсчета Вашего IQ:
instr name=Друг
input name
IQ=(200/(step+(#level$*100)))*100
p Дорогой #%name$! Вы прошли игру 
if level=0 then p на сложном уровне#$
if level=1 then p на среднем уровне#$
if level=2 then p на легком уровне#$ 
pln за #step$ ходов, и Ваш IQ равен #IQ$, С чем я Ваc и поздравляю! 
pln Имейте в виду, что тест примитивен и учитывает всего лишь количество ходов на игру, то есть скорость прохождения - и поправку на уровень сложности. Поэтому не относитесь к результату слишком серьезно... Я лишь добавлю, что по результатам теста получается:#/$
if IQ<50 then pln Вы просто гений! Или бета-тестер! Уверен, вы облазили в игре все закоулки!
if IQ<70 and IQ>=50 then pln Или Вы играете в игру первый раз... Или уровень для Вас легковат... В общем, если ставить себе задачу пройти игру быстрее, то этого можно добиться. Хотя удовольствия будет меньше.
if IQ<90 and IQ>=70 then pln Вы обычно мыслите в правильном направлении! 
if IQ<110 and IQ>=90 then pln Вы опытный игрок!
if IQ<150 and IQ>=110 then pln Ого! Вы умница! Более того - Вы талант!
if IQ<180 and IQ>=150 then pln Вы - Гений?
if IQ>180 and IQ<=200 then pln Вы - Винни-Пух?
if IQ>200 then pln Признавайся, ТЫ ЗНАЛ??? Вы не в первый раз проходите игру! Мой личный рекорд и то меньше:)
btn автор,Послесловие автора
end

:автор
cls
proc grey
pln Послесловие автора: для меня это была очень концептуальная вещь. Во-первых потому, что после прочтения книги Бенджамина Хоффа "Дао Пуха" я заново переосмыслил свое отношение к любимому с детства герою. Во-вторых, потому, что несмотря на небольшие размеры игры я постарался дать всем четырём (Я не ошибся, именно четырем!) действующим персонажам квеста определенный характер и логику поступков и, похоже, это удалось. В-третьих, как Вы, мой друг, уже наверное заметили в этой игре МОЖНО ПОЛОЖИТЬ предмет инвентаря в любом месте и он там будет спокойно лежать, и дождется вашего возвращения, и его при этом спокойно можно будет ВЗЯТЬ снова... если, конечно, согласно логике игры, за это время его кто-нибудь не возьмёт раньше. В-четвертых, в этой игре реализована нехарактерная для меня концепция "герметичного" квеста. Здесь нельзя совершить необратимых поступков. Даже если Вам показалось, что Вы зашли в тупик - не отчаивайтесь, ибо на самом деле, - проверено, - тупиков здесь нет! Игру можно пройти из любого положения, сохранение сделано только для удобства игрока. И, наконец, в пятых: это моя первая игра на URQL:)
pln
p На самом деле это был еще не конец игры, а конец пролога. Но саму большую игру я пока не написал. Sorry!#$
p Напишу наверное, но хочется сначала услышать Ваше мнение. Желательно ругательное. Но если я заслужил хотя бы ма-а-ленькое одобрение - скажите об этом. Пишите на &proc blue&p #$ tightbow@yandex.ru&proc grey&p #$С уважением, Korwin.
anykey k
cls
pln
p А теперь действительно#$ &btn exitgame,КОНЕЦ.
end



:exitgame
end
